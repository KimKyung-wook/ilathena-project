//===== eAthena Script ======================================= 
//= Valkyrie Realm
//===== By: ==================================================
//= Nana, fixes by Poki
//= finished, optimized and tested by Lupus
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 2.9b
//===== Compatible With: ===================================== 
//= eAthena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Quest to become a Tranceded Class. (Advanced/Rebirthed)
//===== Additional Comments: ================================= 
//= 1.1 Made all into functions,additional checks,quest skills,now Valhallana can return you back. She also makes reborn process only when you got no Zeny/items (as in kRO) [Lupus]
//= 1.2 now alternative classes can use their 1-1 job NPC to become Advanced 1st Class [Lupus]
//= 1.4 added Baby Class support [Lupus]
//= 1.5 Fixed loads of gramatical errors. [Mass Zero]
//= 1.6 Added a func which prevent advanced classes passing 2nd Job Quests again. It also guides adv classes where to go. [Lupus]
//= 1.7 On reborn reset some Job Quest Vars and Reset Girls Counter [Lupus]
//= 1.8 Added Taekwondo classes check [Lupus]
//= 1.9 Added support for Job NPC Fase pics, sorrected one dialogue stuff [Lupus]
//= 2.0 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 2.1 Changed all numbers into constants. [Vicious]
//= 2.2 Now ALL quests skills are saved/restored [Lupus]
//= 2.3 Removed Duplicates [Silent]
//= 2.4 Fixed typos, optimized [Lupus]
//= 2.5 Fixed a stuck bug [Lupus]
//= 2.6a Adding proper dialogues [Lupus]
//= 2.7 Rescripted to Aegis 10.3 standards. [L0ne_W0;f]
//=	Removed all functions as they are no longer required.
//=	Correct job level requirements for 1-1 -> 2-x.
//= 2.7a Added Function "F_BlockHigh" back in until rewriteing of the 2-x [L0ne_W0lf]
//=	scripts are comepleted.
//= 2.7b Removed function "F_BlockHigh" as it's no longer used. [L0ne_W0lf]
//= 2.8 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.9 Corrected Valkyrie case spicifying where to warp after rebirthing. [L0ne_W0lf]
//= 2.9b Fixed Verdandi typo, fixed resetting vars [Lupus]
//============================================================ 

valkyrie,48,86,4	script	Valkyrie#	811,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[Valkyrie]";
		mes "Welcome";
		mes "to Valhalla,";
		mes "the Hall of Honor.";
		next;
		mes "[Valkyrie]";
		mes "Please make";
		mes "yourself comfortable";
		mes "while you are here.";
		mes "Honor to the warriors!";
		close;
	}
	else if (Baselevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		mes "[Valkyrie]";
		mes "Welcome";
		mes "to Valhalla,";
		mes "the Hall of Honor.";
		next;
		mes "[Valkyrie]";
		mes "You will now end";
		mes "your present life and";
		mes "begin an entirely new life.";
		mes "Honor to the warriors!";
		next;
		if (Weight > 0 || Zeny > 0 || checkcart() != 0 || checkfalcon() != 0 || checkriding() != 0) {
			mes "[Valkyrie]";
			mes "There are a few things you must";
			mes "do before we start. You must";
			mes "first empty your mind and body.";
			mes "Honor comes when you abandon";
			mes "all your selfish desires...";
			next;
			mes "[Valkyrie]";
			mes "You cannot take anything with you to the next life. Your items, zeny, pets and Pushcart all have to be left behind.";
			next;
			mes "[Valkyrie]";
			mes "When you are ready";
			mes "please return to me,";
			mes "brave adventurer.";
			close2;
			warp "yuno_in02",93,205;
			end;
		}
		else {
			mes "[Valkyrie]";
			mes "I see you've already";
			mes "released yourself from";
			mes "all worldy attachments,";
			mes ""+ strcharinfo(0) +".";
			next;
			mes "[Valkyrie]";
			mes "That's an admirable attitude for an adventurer such as yourself. Honor comes when you abandon all personal desires for the sake of mankind.";
			next;
			if (SkillPoint) {
				mes "[Valkyrie]";
				mes "Hmm... I sense that you have";
				mes "some lingering attachment or";
				mes "unfinished business in your";
				mes "current life. Take care of that,";
				mes "and bring closure to your present life.";
				close2;
				warp "yuno_in02",93,205;
				end;
			}
			mes "[Valkyrie]";
			mes "Now, let me remove all";
			mes "of your present memories...";
			mes "However, you will be able to";
			mes "remember the most honorable";
			mes "moments of this life.";
			next;
			mes "[Valkyrie]";
			mes "With one,";
			mes "I will ask the";
			mes "goddess Urd to remove";
			mes "all of your present";
			mes "memories.";
			next;
			mes "[Valkyrie]";
			mes "With two,";
			mes "I will ask the";
			mes "goddess Verdandi to keep";
			mes "and record the most honorable moments of your present life.";
			next;
			mes "[Valkyrie]";
			mes "With three,";
			mes "I will ask the";
			mes "goddess Skuld to";
			mes "guide you to your";
			mes "next life.";
			next;
			mes "[Valkyrie]";
			mes "One...";
			callfunc "F_ClearJobVar";
			next;
			mes "[Valkyrie]";
			mes "One...";
			mes "Two......";
			//callfunc("F_SaveQuestSkills"); //now not used
			next;
			mes "[Valkyrie]";
			mes "One...";
			mes "Two......";
			mes "And Three.";
			set ADVJOB, Class+Job_Novice_High; //memo the target 3rd Job ID
			if(ADVJOB == Job_Lord_Knight2) set ADVJOB,Job_Lord_Knight;
			if(ADVJOB == Job_Paladin2) set ADVJOB,Job_Paladin;
			jobchange Job_Novice_High;
			resetlvl(1);
			set MISC_QUEST,MISC_QUEST | 1024; //<-reset Skill Reset Event
			skill 142,1,0;
			skill 143,1,0;
			next;
			mes "[Valkyrie]";
			mes "Congratulations.";
			mes "You are now reborn";
			mes "into a brand new life.";
			mes "Please take these small gifts";
			mes "in preparation for your new adventures.";
			getitem 1202,1; //Knife_
			getitem 2302,1; //Cotton_Shirt_
			next;
			mes "[Valkyrie]";
			mes "I wish that the release the goddess Urd has granted you proves to be a blessing. I hope that the memories Verdandi has recorded will always honor you.";
			next;
			mes "[Valkyrie]";
			mes "And I pray that the new life to which the goddess Skuld will guide you will be even more honorable than your last.";
			close2;
			switch (ADVJOB) {
			case 4008:
			case 4015:
				warp "izlude",94,103; break;
			case 4009:
			case 4016:
				warp "prontera",273,354; break;
			case 4010:
			case 4017:
				warp "geffen",120,60; break;
			case 4011:
			case 4019:
				warp "alberta",116,57; break;
			case 4012:
			case 4020:
			case 4021:
				warp "payon",69,100; break;
			case 4013:
			case 4018:
				warp "morocc",154,50; break;
			default:
				warp "yuno_in02",93,205; break;
			}
			end;
		}
	}
	else {
		mes "[Valkyrie]";
		mes "Welcome";
		mes "to Valhalla,";
		mes "the Hall of Honor.";
		next;
		mes "[Valkyrie]";
		mes "Unfortunately, you have not yet been invited here. I ask you to leave immediately. Honor to the warriors!";
		close2;
		warp "yuno_in02",93,205;
		end;
	}
}

yuno_in02,88,164,5	script	메테우스 실프#Library	742,{
	if (Baselevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q == 0) {
			mes "[메테우스 실프]";
			mes "어서오세요. 슈바이체르빌 마법학교";
			mes "도서관입니다. 여기까지 오신 목적은";
			mes "'이미르의 서'를 보기 위해서겠지요?";
			next;
			mes "[메테우스 실프]";
			mes "이미르의 서의 관람은 모두에게 공개되어 있습니다만, 직접 읽으시기를 원하시는 분도 간혹 있지요. 하지만 원본의 상태가 좋지 않기 때문에 열람을 원하시는 분에게는 특별히 기부금을 받고 허가해드립니다.";
			next;
			mes "[메테우스 실프]";
			mes "기부금은 128,500 제니입니다. 서적의 안전한 보관과 고서적 연구를 위해서이니, 열람을 원하신다면 지금 바로 납부하시고 들어가시면 됩니다. 기부하시렵니까?";
			next;
			if (select("기부한다:취소") == 1) {
				if (Zeny > 1284449) {
					set zeny,zeny-1285000;
					set valkyrie_Q,1;
					mes "[메테우스 실프]";
					mes "감사합니다. 기부금은 저희 대학의 연구를 위해 소중히 쓰겠습니다. 들어가셔서 열람하셔도 좋습니다.";
					close;
				}
				mes "[메테우스 실프]";
				mes "충분한 기부금을 가지고 계시지 않습니다. 소지금을 확인해 주세요.";
				close;
			}
			mes "[메테우스 실프]";
			mes "어서오세요. 슈바이체르빌 마법학교 도서관입니다. 천천히 둘러보세요. 여기에는 수많은 서적들이 있습니다.";
			close;
		}
		mes "[메테우스 실프]";
		mes "이미 기부하셨습니다.";
		close;
	}
	mes "[메테우스 실프]";
	mes "어서오세요. 슈바이체르빌 마법학교 도서관입니다. 천천히 둘러보세요. 여기에는 수많은 서적들이 있습니다.";
	close;
}

yuno_in02,93,207,1	script	이미르의 서	111,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[이미르의 서]";
		mes "...영광의 신전으로 들어가는 입구는 다시 태어난 모든 이에게 열려있다. 그것이 영웅의 행로에 도움을 주며 세상 어느 곳으로도 길을 열어준다.";
		next;
		mes "[이미르의 서]";
		mes "또한 그 장소에는 그들을 위한 모든 준비가 되어 있으며, 현실에서는 이루지 못할 염원 또한 그 신전에서 이룰 수 있다고 한다.";
		next;
		if (select("읽기를 중단한다.:계속 읽는다.") == 1) {
			mes "[이미르의 서]";
			mes ".........................";
			close;
		}
		mes "[이미르의 서]";
		mes "하늘과 제일 가까운 곳, 발키리의 신전으로 통하는 길이 있을 터..... 평범한 이들은 영원히 알 수 없으리.";
		close2;
		warp "valkyrie",48,8;
		end;
	}
	else if (Baselevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q != 0) {
			mes "[이미르의 서]";
			mes "...그리하여 선대의 수많은 업적들과 함께 의문시 되는것은, 영웅들에게 필연적으로 닥쳐오는 '죽음'에 관한 문제였다. 영웅의 호칭으로 불리우는 그 누구도 최후의 순간은 있을테니.";
			next;
			mes "[이미르의 서]";
			mes "하지만 그 어떤 영웅에 대한 노래나 기록들, 구두로 전해지는 이야기조차 그 최후의 기록은 남아있지 않았다. 이에 대해 한가지 주목할 점이 있다. 그것은 '라그나로크'에 기술되었던 발키리에 관한 내용들이다.";
			next;
			mes "[이미르의 서]";
			mes "인간으로서 최고의 순간에 위대한 자질을 가진 자는, 최후의 성전에서 전사로 싸우며 그 혼은 발키리의 이끔에 따라 발할라로 갈 것이다. 이는 일반적인 모든 이들의 최후와 달리, 영웅들의 윤회에 관한 것이며";
			next;
			mes "[이미르의 서]";
			mes "진정한 영웅으로 다시 태어나 새로운 시대의 광명을 비추는 자가 되리라. 쓰러진 육신은 다시 일어날 것이며 영혼은 새로운 능력을 일깨우리라. 그러나 불행히도 이미르의 심장은 전란 이후 대륙 전체에 소실되었으며";
			next;
			mes "[이미르의 서]";
			mes "조금씩 발견된 것으로는 우리들의 연구에 허가되지 않았다. 검증되지 않은 그 무엇도 시도할 수 없었다. 그리하여 나는 이 기록을 남기며, 후대에 누군가가 이 기록을 발견하여 그 의지와 희망, 시대의 마지막 빛을";
			next;
			set valkyrie_Q,2;
			mes "[이미르의 서]";
			mes "밝히는 영웅으로 다시 태어나기를 바라며, 다시금 발키리의 신전으로 영웅들을 이끄는 자가 나타나기를 고대하는 바이다. 또한 그 이후에...";
			mes "...........";
			close;
		}
		mes "[이미르의 서]";
		mes "...";
		close;
	}
	mes "[이미르의 서]";
	mes "...";
	close;
}

yuno_in05,49,43,1	script	이미르의 심장	111,{
	if (Baselevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q == 2) warp "valkyrie",48,8;
	}
	end;
}

valkyrie,44,33,5	script	Teleporter	124,{
	if (Upper != 1) {
		set .@karma_d,rand(1,10);
		if (.@karma_d > 4) {
			mes "[Teleporter]";
			mes "Congratulations.";
			mes "Honor to the warriors!";
			close;
		}
		mes "[Teleporter]";
		mes "Please refrain";
		mes "from touching any";
		mes "of the exhibitions.";
		mes "..........";
		close;
	}
	else {
		mes "[Teleporter]";
		mes "Honorable one,";
		mes "which place do you wish to go?";
		next;
		switch(select("Prontera:Morroc:Payon:Geffen:Alberta:Izlude:Al De Baran:Comodo:Juno")) {
		case 1: callsub S_Warpchar,"prontera",116,72;
		case 2: callsub S_Warpchar,"morocc",156,46;
		case 3: callsub S_Warpchar,"payon",69,100;
		case 4: callsub S_Warpchar,"geffen",120,39;
		case 5: callsub S_Warpchar,"alberta",117,56;
		case 6: callsub S_Warpchar,"izlude",94,103;
		case 7: callsub S_Warpchar,"aldebaran",91,105;
		case 8: callsub S_Warpchar,"comodo",209,143;
		case 9: callsub S_Warpchar,"yuno",328,101;
		}
	}
S_WarpChar:
	mes "[Teleporter]";
	mes "Have a nice trip.";
	close2;
	savepoint getarg(0),getarg(1),getarg(2);
	warp getarg(0),getarg(1),getarg(2);
	end;
}

// Warps
//============================================================ 
valkyrie,48,73,0	warp	valk01#1	1,1,valkyrie,48,64
valkyrie,48,66,0	warp	valk01#2	1,1,valkyrie,48,75

