
//    kk  ""tt""  "";;    """"JJ;;kkJJ  JJtt""    ""tt""  ;;tt""""tt    ""tt""   
//   JJ  kk""tt;;JJ      ttkk    ""tt    ;;;;ttJJ;;  ""ttkk;;"";;;;ttkk;;  ;;kk  
//  "";;  ""  kk  kk  tt  ttww    ZZ;;  ww    RR  ;;JJ  JJJJ  tt    RR  ;;JJ  JJ 
//  JJ      """";;""  kk  ttkk  ;;kk    ZZ    ww  ;;""  kk""  ww    kk;;kk""  JJ 
//  kk      JJ  kk    ww  ttkk  JJJJ  ;;ww  ""JJ  ;;tt""RR    ZZ  ;;ZZJJ      kk 
//  kk  ;;  "";;JJ        JJtt  JJJJ  ""kk  JJ""  ww""""ww    ww  ""JJ  ZZ    kk 
//  JJ  JJ  ;;JJ""  "";;  ww;;  kkJJ  JJ""  kk;;  ww  ;;ww  ""JJ  ww;;  ww  ;;tt 
//;;""  RR    RR    ww""  kk""  ;;tt  ww    kkkk      kktt  JJ;;  ZZ;;  ;;  tt   
//  JJkkttkkkk;;kkkk;;JJkktt""kkJJJJkkttJJkk;;  kkkkJJ  ttkkttJJkk;;ttkkJJkkJJ   
//| I | - | L | U | N | A | A | T | H | E | N | A | P | R | O | J | E | C | T |  
//------------------------------------------------------------------------------
// iLAthena 전체 기능 스크립트 - http://www.i-luna.com
// 한글화 by 아이루나 iLAthena Project
//------------------------------------------------------------------------------
// 한글화 by 아이루나(사이버짱)
//------------------------------------------------------------------------------


//////////////////////////////////////////////////////////////////////////////////
// 직업 퀘스트 변수 초기화 기능
//////////////////////////////////////////////////////////////////////////////////

function	script	F_ClearJobVar	{
	// 기타 ---------------------------------
	set JBLVL,0;
	set FIRSTAID,0;
	set PLAYDEAD,0;
	set got_bandage,0;
	set got_novnametag,0;
	// 1차 직업군 ---------------------
	set job_acolyte_q,0; set job_acolyte_q2,0;
	set job_archer_q,0;
	set job_magician_q,0;
	set job_merchant_q,0; set job_merchant_q2,0; set job_merchant_q3,0;
	set job_sword_q,0; set SWTEST, 0;
	set job_thief_q,0;
	// 슈퍼 노비스 ---------------------
	set SUPNOV_Q,0;
	// 2-1차 직업군 ------------------------------
	set ASSIN_Q,0; set ASSIN_Q2,0; set ASSIN_Q3,0;
	set BSMITH_Q,0; set BSMITH_Q2,0;
	set HNTR_Q,0; set HNTR_Q2,0;
	set KNIGHT_Q,0; set KNIGHT_Q2,0;
	set PRIEST_Q,0; set PRIEST_Q2,0; set PRIEST_Q3,0;
	set WIZ_Q,0; set WIZ_Q2,0;
	// 2-2차 직업군 ------------------------------
	set ROGUE_Q,0; set ROGUE_Q2,0;
	set ALCH_Q,0; set ALCH_Q2,0;
	set CRUS_Q,0;
	set MONK_Q,0; set JOB_MONK_C,0;
	set SAGE_Q,0; set SAGE_Q2,0;
	set DANC_Q,0;
	set BARD_Q,0;
	// 확장 직업군
	set TAEK_Q,0; set TK_Q,0;
	set STGL_Q,0;
	set SOUL_Q,0;
	set GUNS_Q,0;
	set NINJ_Q,0;
	return;
}

//////////////////////////////////////////////////////////////////////////////////
// 필요가 없어진 변수 초기화 기능
//////////////////////////////////////////////////////////////////////////////////

function	script	F_ClearGarbage	{
	if(TURTLE==20) set MISC_QUEST,MISC_QUEST | 65536;
	if(MISC_QUEST & 65536) set TURTLE,0;
	set ADV_QSK,0;
	set ADV_QSK2,0;
	set RES_SKILL,0;
	set wizard_m2,0;
	set NEW_MES_FLAG0,0;
	set NEW_MES_FLAG1,0;
	set NEW_MES_FLAG2,0;
	set NEW_MES_FLAG3,0;
	set NEW_MES_FLAG4,0;
	set NEW_MES_FLAG5,0;
	set NEW_LVUP0,0;
	set NEW_LVUP1,0;
	set NEW_JOBLVUP,0;
	set dtseligible,0;
	set MISC_QUEST,MISC_QUEST & ~128;
	return;
}

//////////////////////////////////////////////////////////////////////////////////
// 전직 사용 기능 스크립트
//////////////////////////////////////////////////////////////////////////////////

function	script	Job_Change	{
	jobchange getarg(0),Upper;
	logmes "CLASS CHANGE: " +strcharinfo(0)+ " become a "+jobname(Class);
	return;
}

function	script	F_RandMes	{
	return getarg(rand(1,getarg(0)));
}

function	script	F_SexMes	{
	return getarg(Sex);
}

function	script	F_Hi	{
	return callfunc("F_RandMes",5,"안녕!","안녕하세요!","좋은날 이예요!","오늘 날씨가 좋죠?","오늘 기분 어떠세요?");
}

function	script	F_Bye	{
	return callfunc("F_RandMes",6,"안녕히가세요!","Later.","Goodbye.","Good luck!","Have a nice day!","Byebye!!!");
}

function	script	F_ItemName	{
	set @t$,"^000090";
	if(getarg(3)) set @t$,@t$+"+"+getarg(3)+" ";
	if(getarg(2)==1) set @t$,@t$+"VS ";
	if(getarg(2)==2) set @t$,@t$+"VVS ";
	if(getarg(2)==3) set @t$,@t$+"VVVS ";
	if(getarg(2)>3) set @t$,@t$+getarg(2)+"xVS ";
	if(getarg(1)==1) set @t$,@t$+"Ice ";
	if(getarg(1)==2) set @t$,@t$+"Earth ";
	if(getarg(1)==3) set @t$,@t$+"Fire ";
	if(getarg(1)==4) set @t$,@t$+"Wind ";
	if(getarg(1)>4) set @t$,@t$+"Strange ";
	return @t$+getitemname(getarg(0))+"^000000";
}

function	script	getJobName	{
	return jobname(getarg(0));
}

function	script	F_SaveQuestSkills	{
	set ADV_QSK,0; set ADV_QSK2,0;
	//1st classes quest skills
	for(set @i, 0; @i < 14; set @i, @i + 1){
		if(getskilllv(144+@i)) set ADV_QSK,ADV_QSK|pow(2,@i);
	}
	//2nd classes quest skills
	for(set @i, 0; @i < 19; set @i, @i + 1){
		if(getskilllv(1001+@i)) set ADV_QSK2,ADV_QSK2|pow(2,@i);
	}
	return;
}

function	script	F_Load1Skills	{
	for(set @i, 0; @i < 14; set @i, @i + 1){
		if(ADV_QSK|pow(2,@i) == ADV_QSK) skill 144+@i,1,0;
	}
	set ADV_QSK,0;
	return;
}

function	script	F_Load2Skills	{
	for(set @i, 0; @i < 19; set @i, @i + 1){
		if(ADV_QSK2|pow(2,@i) == ADV_QSK2) skill 1001+@i,1,0;
	}
	set ADV_QSK2,0;
	return;
}
