
//    kk  ""tt""  "";;    """"JJ;;kkJJ  JJtt""    ""tt""  ;;tt""""tt    ""tt""   
//   JJ  kk""tt;;JJ      ttkk    ""tt    ;;;;ttJJ;;  ""ttkk;;"";;;;ttkk;;  ;;kk  
//  "";;  ""  kk  kk  tt  ttww    ZZ;;  ww    RR  ;;JJ  JJJJ  tt    RR  ;;JJ  JJ 
//  JJ      """";;""  kk  ttkk  ;;kk    ZZ    ww  ;;""  kk""  ww    kk;;kk""  JJ 
//  kk      JJ  kk    ww  ttkk  JJJJ  ;;ww  ""JJ  ;;tt""RR    ZZ  ;;ZZJJ      kk 
//  kk  ;;  "";;JJ        JJtt  JJJJ  ""kk  JJ""  ww""""ww    ww  ""JJ  ZZ    kk 
//  JJ  JJ  ;;JJ""  "";;  ww;;  kkJJ  JJ""  kk;;  ww  ;;ww  ""JJ  ww;;  ww  ;;tt 
//;;""  RR    RR    ww""  kk""  ;;tt  ww    kkkk      kktt  JJ;;  ZZ;;  ;;  tt   
//  JJkkttkkkk;;kkkk;;JJkktt""kkJJJJkkttJJkk;;  kkkkJJ  ttkkttJJkk;;ttkkJJkkJJ   
//| I | - | L | U | N | A | A | T | H | E | N | A | P | R | O | J | E | C | T |  
//------------------------------------------------------------------------------
// iLAthena PvP 도어맨 스크립트 - http://www.i-luna.com
// 한글화 by 아이루나 iLAthena Project
//------------------------------------------------------------------------------
// 한글화 by 아이루나
//------------------------------------------------------------------------------

morocc_in,144,138,4	script	PVP가이드#moc	84,{
	callfunc "F_PVPNarrator","morocc_in",141,139;
}

morocc_in,144,142,4	script	PVP도어맨#moc::PVPGateKeeper	83,{
	mes "[PVP도어맨]";
	mes "PVP 대기실로 향하는";
	mes "공간을 열어드리는";
	mes "' PVP 도어맨 ' 입니다";
	mes "PVP 모드에 관한 설명은";
	mes "옆 친구인 가이드에게";
	mes "문의 부탁드립니다 !";
	next;
	switch(select("^FF5533' PVP 나이트메어 모드 '^000000 대기실로:^3355FF' PVP 요요 모드 '^000000 대기실로:^3355FF' PVP 이벤트 모드 '^000000 대기실로:대화를 그만둔다")) {
	case 1:
		mes "[PVP도어맨]";
		mes "PVP 나이트메어 모드는";
		mes "현재 사용이 중지되어";
		mes "있습니다. PVP를 원하시면";
		mes "요요 모드를 이용해주세요.";
		close;
	case 2:
		mes "[PVP도어맨]";
		mes "참가비는 500 Zeny 입니다";
		mes "요요 모드";
		mes "대기실로의 이동을";
		mes "희망하십니까 ?";
		next;
		switch(select("이동:취소")) {
		case 1:
			if (Zeny > 499 && BaseLevel > 30) {
				set zeny,zeny-500;
				warp "pvp_y_room",51,23;
				end;
			}
			else {
				mes "[PVP도어맨]";
				mes "Excuse me, but";
				mes "did you not come prepared?";
				mes "Double check that you have the";
				mes "500 Zeny entrance fee, and";
				mes "that you are at least level 31!";
				close;
			}
		case 2:
			mes "[PVP도어맨]";
			mes "유저와 유저간의";
			mes "실력을 고스란히";
			mes "겨루어 볼 수 있는";
			mes "흥미진진한 PVP 모드 !";
			mes "여러분의 용기를";
			mes "기다리고 있습니다 !";
			close;
		}
	case 3:
		mes "[PVP도어맨]";
		mes "Welcome";
		mes "Please double check";
		mes "That you have the admission or viewing ticket!";
		next;
		if (countitem(7028) == 0 && countitem(7029) == 0) {
			mes "[PVP도어맨]";
			mes "Eh? You don't have it? Then I";
			mes "am sorry, this fight square is";
			mes "only for people who have";
			mes "admission or viewing tickets.";
			mes "You cannot come in without it.";
			close;
		}
		mes "[PVP도어맨]";
		mes "Yes, thank you for participating. Have fun!";
		close2;
		warp "pvp_room",51,23;
		end;
	case 4:
		mes "[PVP도어맨]";
		mes "In this war between Monsters";
		mes "and Humans. This competition";
		mes "between people, The PVP Mode!";
		mes "Encourages us all to become";
		mes "stronger. Come again.";
		mes "We welcome your challenge!";
		close;
	}
}

alberta_in,22,146,4	script	PVP가이드#alb	84,{
	callfunc "F_PVPNarrator","alberta_in",22,148;
}

alberta_in,26,146,4	duplicate(PVPGateKeeper)	PVP도어맨#alb	83

prt_in,56,140,4	script	PVPNarrator#prt	84,{
	callfunc "F_PVPNarrator","prt_in",54,137;
}

prt_in,52,140,4	duplicate(PVPGateKeeper)	PVP도어맨#prt	83

geffen_in,67,63,4	script	PVPNarrator#gef	84,{
	callfunc "F_PVPNarrator","geffen_in",70,59;
}

geffen_in,63,63,4	duplicate(PVPGateKeeper)	PVP도어맨#gef	83

payon_in01,142,50,4	script	PVPNarrator#pay	84,{
	callfunc "F_PVPNarrator","payon_in01",142,46;
}

payon_in01,140,53,4	duplicate(PVPGateKeeper)	PVP도어맨#pay	83


pvp_y_room,86,85,4	script	Fight Square Reception#1	105,{
	set .@mapcount_1,getmapusers("pvp_y_8-1");
	set .@mapcount_2,getmapusers("pvp_y_8-2");
	set .@mapcount_3,getmapusers("pvp_y_8-3");
	set .@mapcount_4,getmapusers("pvp_y_8-4");
	set .@mapcount_5,getmapusers("pvp_y_8-5");
	while(1) {
		switch(select("Prontera [ "+.@mapcount_1+" / 128 ]:Izlude [ "+.@mapcount_2+" / 128 ]:Payon [ "+.@mapcount_3+" / 128 ]:Alberta [ "+.@mapcount_4+" / 128 ]:Morroc [ "+.@mapcount_5+" / 128 ]:Cancel")) {
		case 1:
			callsub S_CheckPVPRoom,@mapcount_1,"pvp_y_8-1";
			break;
		case 2:
			callsub S_CheckPVPRoom,@mapcount_2,"pvp_y_8-2";
			break;
		case 3:
			callsub S_CheckPVPRoom,@mapcount_3,"pvp_y_8-3";
			break;
		case 4:
			callsub S_CheckPVPRoom,@mapcount_4,"pvp_y_8-4";
			break;
		case 5:
			callsub S_CheckPVPRoom,@mapcount_5,"pvp_y_8-5";
			break;
		case 6:
			close;
		}
	}

OnInit:
	waitingroom "Free For All",0;
	end;

S_CheckPVPRoom:
	if (getarg(0) >= 128) {
		mes "[PVP Fight Square Reception Staff]";
		mes "This map is currently full.";
		next;
		return;
	}
	else {
		warp getarg(1),0,0; 
		end;
	}
}

// PVP Nightmare Mode Staff
//============================================================ 
pvp_n_room,30,85,4	script	Fight Square Reception#2	105,{
	set .@mapcount_1,getmapusers("pvp_n_8-1");
	set .@mapcount_2,getmapusers("pvp_n_8-2");
	set .@mapcount_3,getmapusers("pvp_n_8-3");
	set .@mapcount_4,getmapusers("pvp_n_8-4");
	set .@mapcount_5,getmapusers("pvp_n_8-5");
	while(1) {
		switch(select("Sandwich [ "+.@mapcount_1+" / 64 ]:Lock on [ "+.@mapcount_2+" / 32 ]:Four Room [ "+.@mapcount_3+" / 32 ]:Under cross [ "+.@mapcount_4+" / 32 ]:Compass Room [ "+.@mapcount_5+" / 32 ]:Cancel")) {
		case 1:
			callsub S_CheckPVPRoom,@mapcount_1,"pvp_n_8-1",64;
			break;
		case 2:
			callsub S_CheckPVPRoom,@mapcount_2,"pvp_n_8-2",32;
			break;
		case 3:
			callsub S_CheckPVPRoom,@mapcount_3,"pvp_n_8-3",32;
			break;
		case 4:
			callsub S_CheckPVPRoom,@mapcount_4,"pvp_n_8-4",32;
			break;
		case 5:
			callsub S_CheckPVPRoom,@mapcount_5,"pvp_n_8-5",32;
			break;
		case 6:
			close;
		}
	}

OnInit:
	waitingroom "Free For All",0;
	end;

S_CheckPVPRoom:
	if (getarg(0) >= getarg(2)) {
		mes "[PVP Fight Square Reception Staff]";
		mes "This map is currently full.";
		next;
		return;
	}
	else {
		warp getarg(1),0,0; 
		end;
	}
}

// Function 
//============================================================ 
function	script	F_PVPNarrator	{
	mes "[PVP가이드]";
	mes "안녕하세요 !";
	mes "PVP 모드에 관한";
	mes "설명을 담당한";
	mes "PVP 가이드 입니다 !";
	next;
	switch(select("^3355FF' PVP 모드 '^000000 ?:PVP 모드란 ?:PVP 모드의 룰은 ?:현재 위치로 저장한다:대화를 그만둔다")) {
	case 1:
		mes "[PVP가이드]";
		mes "PVP 모드란";
		mes "' Player VS Player Mode '";
		mes "의 약자로서 ..";
		mes "가상의 세계에서 유저간의";
		mes "대전을 통해 짜릿한 긴장감과";
		next;
		mes "[PVP가이드]";
		mes "스펙타클한 전투를 느낄 수 있는";
		mes "일종의 아레나 모드 랍니다";
		mes "제 바로 옆 사람인";
		mes "^3355FF도어맨^000000 에게 대화를";
		mes "시도하시면";
		next;
		mes "[PVP가이드]";
		mes "PVP 모드를 준비할 수";
		mes "있는 대기실로";
		mes "갈 수 있게 됩니다";
		mes "PVP 모드는 레벨 31";
		mes "이상의 유저에게만";
		next;
		mes "[PVP가이드]";
		mes "참가 자격이 부여되며";
		mes "크게 두가지로";
		mes "나이트메어 모드 요요 모드";
		mes "가 있답니다";
		next;
		mes "[PVP가이드]";
		mes "나이트메어 모드에서는";
		mes "던젼룰이 그대로";
		mes "적용되기 때문에";
		mes "아이템을 떨어뜨릴";
		mes "확률이 다소 있으므로";
		next;
		mes "[PVP가이드]";
		mes "이를 숙지하셔서";
		mes "원치않는 불이익을 받지 않으시도록";
		mes "한번 더 생각하시고 입장해주세요.";
		close;

	case 2:
		mes "[PVP가이드]";
		mes "제 옆의 ' PVP도어맨 ' ";
		mes "의 도움을 받으시면";
		mes "500 Zeny 상당의 요금으로";
		mes "PVP 모드의 대기실로";
		mes "이동이 가능하게 됩니다";
		next;
		mes "[PVP가이드]";
		mes "대기실은 크게 두종류인";
		mes "요요 모드와";
		mes "나이트메어 모드로";
		mes "나뉘어져 있으며";
		mes "각 대기실마다";
		next;
		mes "[PVP가이드]";
		mes "PVP 모드의 세계로 안내하는";
		mes "수명의 안내인이 있습니다";
		mes "각 안내인은";
		mes "레벨 31 이상의";
		mes "참여자를 10 레벨씩";
		next;
		mes "[PVP가이드]";
		mes "나누어저 있으며 방은";
		mes "열가지의 PVP 전용 맵중에";
		mes "원하는 곳을 선택하여";
		mes "이동하실 수 있습니다.";
		mes "안내인 머리위에 뜨는";
		next;
		mes "[PVP가이드]";
		mes "창으로 . 제한레벨을";
		mes "알 수 있으며";
		mes "방의 목록에서 그 방에서";
		mes "플레이 하고 있는 인원수를";
		mes "확인 하실 수 있습니다.";
		next;
		mes "[PVP가이드]";
		mes "한 맵에는 맵당 정해진 인원의";
		mes "유저가 참여할 수 있고";
		mes "창에는 ' 참여인원 / 총인원 ' ";
		mes "이라고 뜨게 됩니다";
		next;
		mes "[PVP가이드]";
		mes "참고로 ..";
		mes "전용 맵은 물론";
		mes "대기실에서는";
		mes "위치기억이";
		mes "불가능하오니";
		next;
		mes "[PVP가이드]";
		mes "이점을 꼭 기억하세요.";
		close;

	case 3:
		mes "[PVP가이드]";
		mes "PVP 모드에 진입하게 되면";
		mes "화면 상단에 카운터가";
		mes "뜨게 되는데 이것은";
		mes "현재 해당맵에서의";
		mes "자신의 랭크 / 플레이 인원을";
		next;
		mes "[PVP가이드]";
		mes "나타내는 수치입니다";
		mes "PVP 모드에서의 경험치는";
		mes "오직 PVP 모드에서만";
		mes "작용하며";
		mes "처음 가지고 시작하는";
		next;
		mes "[PVP가이드]";
		mes "기본값은 ' 5 ' 입니다";
		mes "맵안에서 상대방과";
		mes "대전후 승리했을 때";
		mes "얻는 값은 1 이며";
		mes "상대방에게 패했을";
		next;
		mes "[PVP가이드]";
		mes "경우에는 - 5 의";
		mes "패널티를 얻게 됩니다";
		mes "패널티에 의해";
		mes "점수가 0 이하의 수치로";
		mes "떨어졌을 경우에는";
		next;
		mes "[PVP가이드]";
		mes "PVP 모드 바깥세상으로";
		mes "튕겨져 나가게 됩니다";
		mes "하지만 점수가 0 이상이고";
		mes "기절해 있다고 하면";
		mes "다른 동료 또는";
		next;
		mes "[PVP가이드]";
		mes "플레이어의 도움을 받아";
		mes "체력을 회복하여";
		mes "계속 게임에 참가 할 수";
		mes "있게 됩니다";
		next;
		mes "[PVP가이드]";
		mes "상대방과의 대전룰은";
		mes "바깥세상에서의";
		mes "기본적인 전투룰과";
		mes "동일 합니다";
		close;

	case 4:
		mes "[PVP가이드]";
		mes "위치 저장에";
		mes "성공하였습니다";
		mes "이용해 주셔서";
		mes "감사합니다";
		close2;
		savepoint getarg(0),getarg(1),getarg(2);
		end;
	case 5:
		mes "[PVP도어맨]";
		mes "유저와 유저간의";
		mes "실력을 고스란히";
		mes "겨루어 볼 수 있는";
		mes "흥미진진한 PVP 모드 !";
		mes "여러분의 용기를";
		mes "기다리고 있습니다 !";
		close;
	}
}

// Event Contestant Entrance
//============================================================ 
pvp_room,54,85,4	script	Registration Staff#1	105,{
	if (countitem(7028) == 0) {
		mes "[PVP Combat Square Register Staff]";
		mes "Eh? How did you get in here?";
		mes "This is the entrance for players only.";
		mes "For details about viewers please proceed to the Register Staff on your right.";
		close;
	}

	if (.pvp_close_n_room == 1 && .pvp_close_s_room == 1 && .pvp_close_w_room == 1 && .pvp_close_e_room == 1) {
		Set .pvp_close_n_room,0;
		Set .pvp_close_s_room,0;
		Set .pvp_close_w_room,0;
		Set .pvp_close_e_room,0;
	}

	if (select("Combat Square one :Cancel") == 1) {
		mes "[PVP Combat Square Register Staff]";
		mes "'"+ strcharinfo(0) +"'";
		mes "Are you ready?!";
		next;
		if (select("Yes!:No!") == 1) {
			mes "[PVP Combat Square Register Staff]";
			mes "OK! I will send you inside";
			mes "Good luck!";
			close2;
			delitem 7028,1; //Invite_For_Duel
			while(1) {
				switch(rand(1,4)) {
				case 1:
					if (.pvp_close_n_room == 0) {
						set .pvp_close_n_room,1;
						warp "pvp_2vs2",40,59;
						end;
					}
				case 2:
					if (.pvp_close_s_room == 0) {
						set .pvp_close_s_room,1;
						warp "pvp_2vs2",59,40;
						end;
					}
				case 3:
					if (.pvp_close_w_room == 0) {
						set .pvp_close_w_room,1;
						warp "pvp_2vs2",20,40;
						end;
					}
				case 4:
					if (.pvp_close_e_room == 0) {
						set .pvp_close_e_room,1;
						warp "pvp_2vs2",40,20;
						end;
					}
				}
			}
		}
		mes "[PVP Combat Square Register Staff]";
		mes "Come back anytime you are ready.";
		close;
	}
	close;

OnInit:
	Set .pvp_close_n_room,0;
	Set .pvp_close_s_room,0;
	Set .pvp_close_w_room,0;
	Set .pvp_close_e_room,0;
	waitingroom "Combat Square players entrance only",0;
	end;
}

// Spectator Entrance
//============================================================ 
pvp_room,62,85,4	script	Spectator's Entrance#1::PVPSpectator	105,{
	if (countitem(7029) == 0) {
		mes "[PVP Compete Square Register Staff]";
		mes "This is the entrance for viewers.";
		mes "For details about players entrance please proceed to the Register Staff on your left.";
		close;
	}

	if (select("Compete Square one:Cancel") == 1) {
		mes "[PVP Combat Square Register Staff]";
		mes "You got it, thanks for participating. Have fun!";
		close2;
		delitem 7029,1; //Admission_For_Duel
		switch(rand(1,4)) {
		case 1: warp "pvp_2vs2",39,7; end;
		case 2: warp "pvp_2vs2",39,73; end;
		case 3: warp "pvp_2vs2",7,39; end;
		case 4: warp "pvp_2vs2",73,39; end;
		}
	}
	close;

OnInit:
	waitingroom "Compete Square viewer's entrance",0;
	end;
}

pvp_room,70,85,4	duplicate(PVPSpectator)	Spectator's Entrance#2	105

pvp_room,78,85,4	duplicate(PVPSpectator)	Spectator's Entrance#3	105

pvp_room,86,85,4	duplicate(PVPSpectator)	Spectator's Entrance#4	105

// Spectator Warps
//============================================================ 
pvp_2vs2,5,4,0	script	Combat Square Staff#1::PVPSpecWarp	45,1,1,{
OnTouch:
	mes "[Combat Square Staff]";
	mes "May I help you?";
	if (select("To the center viewer seat.:Leave Combat Square.") == 1) {
		warp "pvp_2vs2",38,38;
		end;
	}
	warp "pvp_room",84,39;
	end;
}

pvp_2vs2,5,74,0	duplicate(PVPSpecWarp)	Combat Square Staff#2	45,1,1

pvp_2vs2,74,74,0	duplicate(PVPSpecWarp)	Combat Square Staff#3	45,1,1

pvp_2vs2,74,5,0	duplicate(PVPSpecWarp)	Combat Square Staff#4	45,1,1

pvp_2vs2,40,40,0	script	Combat Square Staff#5	45,1,1,{
	set mapmove,0;
	mes "[Combat Square Staff]";
	mes "May I help you?";
	if (select("To the side viewer seat.:Leave Combat Square.") == 1) {
		switch(rand(1,4)) {
		case 1: warp "pvp_2vs2",39,7; end;
		case 2: warp "pvp_2vs2",39,73; end;
		case 3: warp "pvp_2vs2",7,39; end;
		case 4: warp "pvp_2vs2",73,39; end;
		}
	}
	warp "pvp_c_room",84,39;
	end;
}

// PVP Area Exit warp
//============================================================ 
pvp_room,51,19,0	script	out#eventpvp	45,4,2,{
OnTouch:
	mes "[Combat Square Staff]";
	mes "Did you have fun in Combat Square?";
	mes "May I ask where you want to go?";
	next;
	switch(select("prontera.:morocc.:geffen.:payon.:alberta.:Cancel.")) {
	case 1: warp "prontera",107,60; end;
	case 2: warp "morocc",157,96; end;
	case 3: warp "geffen",120,36; end;
	case 4: warp "payon",96,100; end;
	case 5: warp "alberta",41,243; end;
	case 6: close;
	}
}
