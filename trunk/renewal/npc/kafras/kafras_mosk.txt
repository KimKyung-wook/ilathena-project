
//    kk  ""tt""  "";;    """"JJ;;kkJJ  JJtt""    ""tt""  ;;tt""""tt    ""tt""   
//   JJ  kk""tt;;JJ      ttkk    ""tt    ;;;;ttJJ;;  ""ttkk;;"";;;;ttkk;;  ;;kk  
//  "";;  ""  kk  kk  tt  ttww    ZZ;;  ww    RR  ;;JJ  JJJJ  tt    RR  ;;JJ  JJ 
//  JJ      """";;""  kk  ttkk  ;;kk    ZZ    ww  ;;""  kk""  ww    kk;;kk""  JJ 
//  kk      JJ  kk    ww  ttkk  JJJJ  ;;ww  ""JJ  ;;tt""RR    ZZ  ;;ZZJJ      kk 
//  kk  ;;  "";;JJ        JJtt  JJJJ  ""kk  JJ""  ww""""ww    ww  ""JJ  ZZ    kk 
//  JJ  JJ  ;;JJ""  "";;  ww;;  kkJJ  JJ""  kk;;  ww  ;;ww  ""JJ  ww;;  ww  ;;tt 
//;;""  RR    RR    ww""  kk""  ;;tt  ww    kkkk      kktt  JJ;;  ZZ;;  ;;  tt   
//  JJkkttkkkk;;kkkk;;JJkktt""kkJJJJkkttJJkk;;  kkkkJJ  ttkkttJJkk;;ttkkJJkkJJ   
//| I | - | L | U | N | A | A | T | H | E | N | A | P | R | O | J | E | C | T |  
//------------------------------------------------------------------------------
// iLAthena 모스코비아 카프라 직원 스크립트 <http://www.i-luna.com>
// 한글화 by 아이루나 iLAthena Project
//------------------------------------------------------------------------------
// 한글화 by 아이루나(사이버짱)
//------------------------------------------------------------------------------

moscovia,223,191,4	script	카프라 직원#mosk	114,{
	cutin "kafra_04",2;
	mes "[카프라 직원]";
	mes "어서오십시오. 카프라 서비스는 언제나 여러분 곁에 있습니다. 무엇을 도와드릴까요?";
	next;
	switch(select("저장 서비스 이용:창고 서비스 이용:카트 서비스 이용:기타 정보 확인:취소")) {
		case 1:
			mes "[카프라 직원]";
			mes "모스코비아에 저장 되었습니다. 이용해 주셔서 감사합니다.";
			savepoint "moscovia",221,194;
			close2;
			cutin "", 255;
			end;
		case 2:
			if(basicskillcheck() && getskilllv("NV_BASIC") < 6){
				mes "[카프라 직원]";
				mes "죄송합니다. 고객님.";
				mes "창고를 이용하시려면 초보자 스킬레벨을 6 이상 올리셔야 합니다.";
				close2;
				cutin "", 255;
				end;
			}
			if(countitem(7059)) delitem 7059,1;
			else {
				if(Zeny<80){
					mes "[카프라 직원]";
					mes "고객님. 지불하실 금액이 부족합니다.";
					mes "창고 서비스 이용료는 80 제니 입니다.";
					close2;
					cutin "", 255;
					end;
				}
				set Zeny, Zeny-80;
				set RESRVPTS, RESRVPTS + (80/5);
			}
			mes "[카프라 직원]";
			mes "창고를 열겠습니다. 이용해 주셔서 감사합니다.";
			
			callfunc("F_CheckKafCode");
			
			close2;
			openstorage;
			cutin "", 255;
			end;
		case 3:
			if(baseClass != Job_Merchant){
				mes "[카프라 직원]";
				mes "죄송합니다.";
				mes "카트는 상인과 블랙스미스, 알케미스트 전용입니다.";
				close2;
				cutin "", 255;
				end;
			}

			else if(checkcart() == 1){
				mes "[카프라 직원]";
				mes "고객님은 현재 카트를 이용하시고 있습니다.";
				close2;
				cutin "", 255;
				end;
			}

			if(countitem(7061) > 0) delitem 7061,1;
			else {
				mes "[카프라 직원]";
				mes "카트 이용요금은 700 Zeny입니다. 카트를 대여하시겠습니까?";
				next;
				if(select("카트 서비스 이용:취소") == 1) {
					if(Zeny<700){
						mes "[카프라 직원]";
						mes "고객님,돈이 충분하지 않습니다.";
						mes "카트 이용요금은 700 Zeny입니다.";
						close2;
						cutin "", 255;
						end;
					}
					set Zeny,Zeny-700;
					set RESRVPTS, RESRVPTS + 48;
				}else{
					close2;
					cutin "", 255;
					end;
				}
			}
			setcart;
			close2;
			cutin "", 255;
			end;
		case 4:
			if(select("적립포인트 확인:취소") == 2) {
				cutin "", 255;
				close;
			}
			mes "[카프라 직원]";
			mes "" +strcharinfo(0) + " 고객님의 적립포인트는 ^0000ff"+RESRVPTS+"^000000점 입니다.";
			next;
			mes "[카프라 직원]";
			mes "적립포인트는 저희 카프라 본사에서 상품과 교환이 가능합니다.";
			mes "편리한 저희 카프라 서비스도 이용하시고 많은 상품도 받아가세요~";
			close2;
			cutin "", 255;
			end;
		case 5:
			mes "[카프라 직원]";
			mes "이용해 주셔서 감사합니다.";
	    		mes "저희 카프라 서비스는 보다 완벽한 서비스와 엄격한 관리 업무로 고객 여러분이 안심하고 편하게 이용하실 수 있도록 최선을 다하고 있습니다.";
			close2;
			cutin "", 255;
			end;
	}
}
