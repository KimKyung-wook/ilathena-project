
//    kk  ""tt""  "";;    """"JJ;;kkJJ  JJtt""    ""tt""  ;;tt""""tt    ""tt""   
//   JJ  kk""tt;;JJ      ttkk    ""tt    ;;;;ttJJ;;  ""ttkk;;"";;;;ttkk;;  ;;kk  
//  "";;  ""  kk  kk  tt  ttww    ZZ;;  ww    RR  ;;JJ  JJJJ  tt    RR  ;;JJ  JJ 
//  JJ      """";;""  kk  ttkk  ;;kk    ZZ    ww  ;;""  kk""  ww    kk;;kk""  JJ 
//  kk      JJ  kk    ww  ttkk  JJJJ  ;;ww  ""JJ  ;;tt""RR    ZZ  ;;ZZJJ      kk 
//  kk  ;;  "";;JJ        JJtt  JJJJ  ""kk  JJ""  ww""""ww    ww  ""JJ  ZZ    kk 
//  JJ  JJ  ;;JJ""  "";;  ww;;  kkJJ  JJ""  kk;;  ww  ;;ww  ""JJ  ww;;  ww  ;;tt 
//;;""  RR    RR    ww""  kk""  ;;tt  ww    kkkk      kktt  JJ;;  ZZ;;  ;;  tt   
//  JJkkttkkkk;;kkkk;;JJkktt""kkJJJJkkttJJkk;;  kkkkJJ  ttkkttJJkk;;ttkkJJkkJJ   
//| I | - | L | U | N | A | A | T | H | E | N | A | P | R | O | J | E | C | T |  
//------------------------------------------------------------------------------
// iLAthena 도둑 전직 스크립트 <http://www.i-luna.com>
// 한글화 by 아이루나 iLAthena Project
//------------------------------------------------------------------------------
// 한글화 by 인즈
// 수정 by 이훈희
//------------------------------------------------------------------------------

moc_prydb1,39,129,2	script	도둑 길드 관계자	69,{
	if (Upper == 1) {
		if (ADVJOB == Job_Assassin_Cross || ADVJOB == Job_Stalker) {
			if (Class == Job_Novice_High) {
				mes "[도둑 길드 관계자]";
				mes "어라? 너 내가 아는 사람인가? 이거 소름이 돋을 정도로 기시감이 드는데? 너 쌍둥이 형제 없지?";
				next;
				if (getskilllv("NV_BASIC") < 9) {
					mes "[도둑 길드 관계자]";
					mes "뭐? 도둑이 되고 싶다고? 미안하지만 아직 완벽히 준비가 되지 않은 것 같은데.";
					next;
					mes "[도둑 길드 관계자]";
					mes "기본 기능이나 모두 배워오라구. 아무튼, 나중에 보자!";
					close;
				}
				mes "[도둑 길드 관계자]";
				mes "음... 투쟁의 삶을 지나온 너에게, 이 순간에 도둑이 되게 해주겠어.";
				mes "네가 바라는 것보다 더욱 멋진 사나이가 되도록...";
				next;
				skill 143,0,0;
				jobchange Job_Thief_High;
				skill 149,1,0;
				skill 150,1,0;
				skill 151,1,0;
				skill 152,1,0;
				mes "[도둑 길드 관계자]";
				mes "자, 이제 끝이야.";
				mes "그럼 다음에 보자.";
				close;
			}
			else {
				mes "[도둑 길드 관계자]";
				mes "...무슨 볼일?";
				close;
			}
		}
		else {
			mes "[도둑 길드 관계자]";
			mes "...어이! 도둑이 되고 싶어!!";
			mes "...라거나 하지는 않겠지?";
			next;
			mes "[도둑 길드 관계자]";
			mes "난 바빠! 다음에 보자!";
			close;
		}
	}
	if (BaseJob == Job_Thief) {
		mes "[도둑 길드 관계자]";
		mes "뭐, 애로사항이 있으면 언제든 말하라고.";
		mes "...딱히 도움을 줄 수는 없지만.";
		close;
	}
	else if (BaseJob != Job_Novice && BaseJob != Job_Thief) {
		mes "[도둑 길드 관계자]";
		mes "이건 뭐야...?";
		next;
		mes "[도둑 길드 관계자]";
		switch(Class) {
		case 1:
			mes "무식한 검사잖아...?";
			break;
		case 2:
			mes "서커스 단원 마술사잖아...?";
			break;
		case 3:
			mes "바보 궁수잖아...?";
			break;
		case 4:
			mes "세계최강 연예인 숭배자 복사잖아...?";
			break;
		case 5:
			mes "악질 상인이잖아?";
			break;
		case 8:
			mes "왜 성직자가 이런 곳에? 내 잘못을 참회하면 새 출발을 할 수 있을 것 같은 느낌이랄까?";
			close;
		case 12:
			mes "우리랑 함께하고 싶다고?! 이봐, 당신은 변했어. 정말 위험해 보인다고...";
			close;
		case 17:
			mes "당신이 가진 기술은 내가 꿈꾸던 것이야!";
			close;
		}
		next;
		mes "[도둑 길드 관계자]";
		mes "*씨익*";
		mes "이봐, 자네는 이런 곳에 올 이유가 전혀 없는 것 같은데?";
		mes "당신이 있어야할 곳으로 가.";
		close;
	}
	if (job_thief_q == 3 && countitem(1069) > 0 || countitem(1070) > 0) {
		mes "[도둑 길드 관계자]";
		mes "아라....? 괜히 해본 말인데 정말로 버섯을 캐왔네.";
		mes "너, 바보지? 넌 이제까지 속았어, 알아?";
		next;
		mes "[도둑 길드 관계자]";
		mes ".... 민망한 농담은 그만두지.";
		mes "이 곳은 도둑이 되려는 바보들을 접수 받는 곳이야. 전직을 하려면 옆 녀석에게 문의해 봐.";
		close;
	}
	else if (job_thief_q == 3) {
		mes "[도둑 길드 관계자]";
		mes "여어, 버섯 농장은 어땠어?";
		mes "재밌었어?";
		next;
		if (select("아아, 무척.:..무서웠어.") == 1) {
			mes "[도둑 길드 관계자]";
			mes "흐흐! 아주 좋은 자세야. 우리 쪽의 일을 할 때는 너처럼 손이 더러워 지는 것을 두려워하면 안되지.";
			close;
		}
		mes "[도둑 길드 관계자]";
		mes "흐음? 나도 그곳에 다녀온 적 있는데...";
		mes "(중얼중얼)";
		close;
	}
	if (job_thief_q == 2) {
		mes "[도둑 길드 관계자]";
		mes "....? 여기서 뭐하고 있는거야..? 버섯 캐러 가야지?";
		mes "아니면 전직조건을 모르는 거야? 다시 알려줘?";
		next;
		if  (select("응.:아니, 됐어.") == 1) {
			mes "[도둑 길드 관계자]";
			mes "*씨익*";
			mes "이그~~ 아무든 너처럼 뒷북 치는 녀석이 꼭 한 명씩 있다니깐. 다시 설명 해줄테니깐 잘들어.";
			next;
			mes "[도둑 길드 관계자]";
			mes "모로크에서 가장 악랄하기로 유명한 상인 '시부'가 경영하고 있는 버섯농장에서 버섯을 훔쳐오는 것이 전직조건이다!";
			next;
			mes "[도둑 길드 관계자]";
			mes "훔쳐와야 할 버섯은 ^0000FF주홍털 그물버섯^000000과 ^0000FF주홍털_끈적버섯^000000 두 종류.";
			next;
			mes "[도둑 길드 관계자]";
			mes "버섯농장엔 버섯을 지키는 몬스터들이 있다는 거 알고 있지? 전래동화속 노부부의 기분으로 버섯만 채집할 생각은 아에 처음부터 버리고 가는 것이 좋아.";
			next;
			mes "[도둑 길드 관계자]";
			mes "버섯을 채집해 다시 이 곳 도둑길드로 돌아오면 채점을 하겠는데 주홍털 그물버섯 1 개당 3 점씩, 주홍털 끈적버섯 1 개당 1 점씩 가산되며 모두 합해 25 점 이상이 된다면 도둑으로의 전직을 승인해 줄께.";
			next;
			mes "[도둑 길드 관계자]";
			mes "장소는 피라미드 입구 반대 방향으로 주욱 걸어가다 보면 두 개의 기둥 사이에 우리 동료가 있어.";
			next;
			mes "[도둑 길드 관계자]";
			mes "녀석에게 말을 걸면 버섯농장 뒷구멍을 통해서 버섯농장에 보내줄거야.";
			mes "정확한 좌표를 알려줬는데.. 아마 '^FF0000141, 125^000000' 라고 했던거 같아.";
			next;
			mes "[도둑 길드 관계자]";
			mes "좌표를 알고 싶으면 대사창에 '/좌표' 라고 적고 엔터를 누르면 현재 네가 있는 좌표가 표시되니 참고해 둬.";
			close;
		}
		mes "[도둑 길드 관계자]";
		mes "..... 그럼.. 뭐야..? 나에게 할 말이라도 있으니깐 말을 걸었을거 아니야? 그게 아니면, 지금 시비거는 거야...? 앙?";
		close;
	}
	mes "[도둑 길드 관계자]";
	if (job_thief_q == 0) {
		mes "헤에... 보아하니 어디 유명한 부자집 자식 같은데 무엇이 아쉬워서 이런 지저분한 곳까지 왔을까..?";
	}
	else {
		mes "여~ 다시 왔군. 이번엔 확실하겠지?";
	}
	next;
	if (select("도둑으로 전직한다!:그만둔다.") == 1) {
		if (job_thief_q == 0) {
			mes "[도둑 길드 관계자]";
			mes "어이구~ 자랑이다 자랑이야. 도둑이 되겠다고 아무런 주저없이 당당하게 말하다니. 아에 동네방네 다 떠들고 다니지 그래?";
			next;
			mes "[도둑 길드 관계자]";			
			mes "'아하하하. 전 남의 물건이나 훔치고 다니는 자랑스러운 도둑이 되고 오겠습니다.'라고 써 있는 현수막도 만들어 마을 입구에도 달아놓고 말야.";
			next;
			mes "[도둑 길드 관계자]";
			mes "그렇게 도둑이 되고 싶었어?";
			next;
			switch(select("응.:아니.:그렇게 말하는 너는?")) {
			case 1:
				mes "[도둑 길드 관계자]";
				mes "그래...? 거.. 참... 살다보니 별 이상한 사람을 다 보는군.";
				break;
			case 2:
				mes "[도둑 길드 관계자]";
				mes "그럼 뭐야...? 애인이 여기 소속이야..? 아니면 도둑이라는 직업이 능력이 좋아 보여서..?";
				break;
			case 3:
				mes "[도둑 길드 관계자]";
				mes "에..? 나? 나 말야?";
				mes "뭐... 나야 성격상 도둑체질이다 보니 그런거고...  ...그런 사소한건 어떻게 되든 상관없어.";
				break;
			}
			next;
			mes "[도둑 길드 관계자]";
			mes "아무튼 현실세계에서 도둑이 되겠다느니 하면 절대 안돼!";
			mes "어쨌든 전직 할거야?";
			next;
			switch(select("전직 신청을 한다.:그만둔다!")) {
			case 1:
				mes "[도둑 길드 관계자]";
				mes "그렇다면 네 이름을 말해. 응? 뭐라고?";
				next;
				mes "[도둑 길드 관계자]";
				mes "" + strcharinfo(0) + ". " + strcharinfo(0) + " 이라고...?";
				set job_thief_q,1;
				next;
				mes "[도둑 길드 관계자]";
				mes "신청서는 문제없이 접수 되었다.";
				mes "아, 전직조건을 미리 알고 준비해 왔다면 바로 면접시험으로 들어갈 수 있는데.";
				mes "밑여야 본전! 해볼래?";
				next;
				switch(select("응, 해볼게.:아니, 아직 괜찮아.")) {
				case 1:
					break;
				case 2:
					mes "[도둑 길드 관계자]";
					mes "뭐... 좋을대로. 준비되면 다시와.";
					close;
				}
				break;
			case 2:
				mes "[도둑 길드 관계자]";
				mes "...... 전직 신청을 안하면 전직 할 수 없다는 것은 잘 알고 있을텐데.....";
				mes "무슨 생각을 하고 있는거야..?";
				close;
			}
		}
		else {
			mes "[도둑 길드 관계자]";
			mes "잠시만 기다려.";
			next;
		}
		mes "[도둑 길드 관계자]";
		mes "이름이.... " + strcharinfo(0) + ". 에..... 어디있나....";
		mes "아. 있군, 있어. 이름 " + strcharinfo(0) + ".";
		mes "어디...";
		next;
		mes "[도둑 길드 관계자]";
		if (getskilllv("NV_BASIC") < 9) {
			mes "도둑이 되겠다는 너의 굳건한 의지를 잘 알겠다만......";
			mes "의지만으로 코흘리개 어린이에게 총대를 매어주는건 안되겠지? 최소 초보자 스킬 9 이상 올리고 와.";
			close;
		}
		mes "으음.... 흥신소 자료를 보니 흥미있는 전과도 있겠다 어느정도 기본 능력도 갖춰고 하니... 좋아.";
		next;
		mes "[도둑 길드 관계자]";
		mes "이름 " + strcharinfo(0) + ". 도둑길드의 면접은 합격이다!";
		set job_thief_q,2;
		next;
		mes "[도둑 길드 관계자]";
		mes "자, 그렇다면 도둑으로써 소질이 있는지 확인해 봐야겠지?";
		mes "시험에 대해선 알고 있나..?";
		next;
		switch(select("알아.:몰라.")) {
		case 1:
			mes "[도둑 길드 관계자]";
			mes "그래? 거 잘 됐군.";
			break;
		case 2:
			mes "[도둑 길드 관계자]";
			mes "하아? ....모른다면 알려줘야지.";
			next;
			mes "[도둑 길드 관계자]";
			mes "모로크에서 가장 악랄하기로 유명한 상인 '시부'가 경영하고 있는 버섯농장에서 버섯을 훔쳐오는 것이 전직조건이다!";
			next;
			mes "[도둑 길드 관계자]";
			mes "훔쳐와야 할 버섯은 ^0000FF주홍털 그물버섯^000000과 ^0000FF주홍털_끈적버섯^000000 두 종류.";
			next;
			mes "[도둑 길드 관계자]";
			mes "버섯을 채집해 다시 이 곳 도둑길드로 돌아오면 채점을 하겠는데 주홍털 그물버섯 1 개당 3 점씩, 주홍털 끈적버섯 1 개당 1 점씩 가산되며 모두 합해 25 점 이상이 된다면 도둑으로의 전직을 승인해 줄께.";
			next;
			mes "[도둑 길드 관계자]";
			mes "장소는 피라미드 입구 반대 방향으로 주욱 걸어가다 보면 두 개의 기둥 사이에 우리 동료가 있어.";
			next;
			mes "[도둑 길드 관계자]";
			mes "녀석에게 말을 걸면 버섯농장 뒷구멍을 통해서 버섯농장에 보내줄거야.";
			next;
			mes "[도둑 길드 관계자]";
			mes "정확한 좌표를 알려줬는데.. 아마 '^FF0000141, 125^000000' 라고 했던거 같아.";
			next;
			mes "[도둑 길드 관계자]";
			mes "좌표를 알고 싶으면 대사창에 '/좌표' 라고 적고 엔터를 누르면 현재 네가 있는 좌표가 표시되니 참고해 둬.";
			break;
		}
		next;
		mes "[도둑 길드 관계자]";
		mes "남은건 두 가지 사항뿐이군.";
		next;
		mes "[도둑 길드 관계자]";
		mes "버섯농장엔 버섯을 지키는 몬스터들이 있다는 거 알고 있나?";
		mes "전래동화 속 노부부의 버섯 캐는 기분으로 버섯만 캐갈 생각은 아예 처음부터 버리는게 좋아.";
		next;
		mes "[도둑 길드 관계자]";
		mes "어떻게 적당히 처리해가면서 버섯을 캘 것인가를 생각해 두는게 좋을거야.";
		next;
		mes "[도둑 길드 관계자]";
		mes "다음은 버섯에 대한 것인데 아무리 전직시험이라곤 하지만 우리가 하는 짓은 도.둑.질. 인거 알고 있지?";
		next;
		mes "[도둑 길드 관계자]";
		mes "전직을 하기 위해선 어쩔수 없이 버섯을 캐면서 악해져야 한다는 점. 그 점이 싫다면 도둑자격 미달이지. 암, 그렇고 말고.";
		next;
		mes "[도둑 길드 관계자]";
		mes "더 이상 내가 해 줄 이야기는 없어. 그럼, 슬슬 가보는게 어때?";
		close;
	}
//	close;
}

moc_prydb1,42,133,2	script	길드 관계자#thief	118,{
	if (BaseJob == Job_Thief) {
		mes "[브레드]";
		mes "지금은 특별한 일이 없으니 나중에 와. 그때 연락줄테니까.";
		close;
	}
	else if (BaseJob != Job_Novice && BaseJob != Job_Thief) {
		mes "[도둑 길드 관계자]";
		mes "어이~ 어이~ 거기 다른 직업에 종사하고 있는 인간아.";
		next;
		mes "[도둑 길드 관계자]";
		mes "여기서 뭘 하고 있는거야? 여긴 너희 집 안방이 아니야. 휘이, 얼른 나가.";
		close;
	}
	if (job_thief_q == 0 || job_thief_q == 1) {
		mes "[브레드]";
		mes "어이구? 초보자가 이 곳엔 무슨 찾아왔나?";
		mes "혹시 도둑으로 전직하고 싶어서 온 것이면 내 옆 성질 더러운 누나에게 물어보면 자세히 알려줄거야.";
		close;
	}
	else if (job_thief_q == 2) {
		mes "[브레드]";
		mes "헤에. 면접은 잘 봤나 보네?";
		mes "그렇다면 버섯 캐오기 시험을 보겠군. 잘 보고 와.";
		close;
	}
	else if (job_thief_q == 3) {
		mes "[브레드]";
		mes "음..? ";
		mes "아, 왔군, 왔어. 그래, 어디 얼마나 훔쳐왔나 볼까?";
		if (countitem(1069) == 0 && countitem(1070) == 0) {
			next;
			mes "[브레드]";
			mes "...... 빈 손이냐!! 앙?! 당장 나가! 나가서 아무 버섯이나 가져와!";
			mes "너 인마, 아무 것도 안 가져오면 도둑으로 전직할 생각마! 알았어?!";
			close;
		}
		next;
		set .@thief_item1,countitem(1069) * 3;
		set .@thief_item2,countitem(1070);
		set .@total_thief,.@thief_item1 + .@thief_item2;
		set .@money_thief,((.@thief_item1 * 5) + (.@thief_item2 * 2)) + 200;
		mes "[브레드]";
		if (countitem(1069) != 0) {
			mes "주홍털 그물버섯..." + countitem(1069) + "개.";
		}
		if (countitem(1070) != 0)
			mes "주홍털 끈적버섯..." + countitem(1070) + "개.";
		next;
		mes "[브레드]";
		mes "채점을 해보면 총점이...";
		next;
		mes "[브레드]";
		mes "" + .@total_thief + ".";
		next;
		mes "[브레드]";
		if (.@total_thief > 25) {
			mes "오호, 25점 이상이라니,";
			mes "간만에 쓸만한 녀석이 들어왔군!";
			mes "좋아, 합격!";
		}
		else if (.@total_thief == 25) {
			mes "정확히 25점이다!";
			mes "좋아, 합격!";
		}
		else {
			mes "...좀 더 가져와!";
			mes "(버럭)";
			close;
		}
		next;
		mes "[브레드]";
		mes "총수령님으로부터의 전언이다.";
		next;
		mes "[도둑길드 총수령 'E']";
		mes strcharinfo(0) + ".";
		mes "'제시한 모든 시험을 통과했으므로 도둑으로 인정, 동시에 도둑길드의 정식입단을 허락한다.'";
		if (countitem(1069) != 0) delitem 1069,countitem(1069); //Mushroom_Of_Thief_1
		if (countitem(1070) != 0) delitem 1070,countitem(1070); //Mushroom_Of_Thief_2
		callfunc "Job_Change",Job_Thief;
		callfunc "F_ClearJobVar";
		next;
		mes "[도둑길드 총수령 'E']";
		mes "'이후 도둑길드의 일원으로 태만하지 않고 질서와 규범을 준수해라.'";
		mes "'만약, 벗어나는 행동에 의해 도둑길드에 해가 되었을 경우 실력행사에 의해서 제거당한다는 것을 부디 잊지 말도록.'";
		mes "'아무조록, 멋진 활동을 기대하겠다. 이상.'";
		next;
		mes "[브레이드]";
		mes "히~하~! 입단을 축하한다! 동지여.";
		mes "내 이름은 '브레이드'. 이 곳 도둑길드에서 전직 및 길드회원 관리를 담당하고 있지.";
		mes "지금은 어떻지 모르겠지만 나중엔 자주보게 될거야.";
		next;
		mes "[브레드]";
		mes "자. 이거 받아. 신입에게 지급되는 보조금에다 네가 가져온 버섯 값이다.";
		set zeny,zeny+.@money_thief;
		mes "[브레이드]";
		mes "자, 해줄건 다 해주었으니 이제 네 볼 일을 봐. 난 할 일이 많은 몸이거든.";
		mes "그럼, 다음에 보자.";
		close;
	}
}

moc_ruins,141,125,3	script	수상한 사람#1	83,{
	if (BaseJob == Job_Thief) {
		mes "[도둑 길드 관계자]";
		mes "오호~ 멋진 도둑이 되었군.";
		mes "하지만, 이 곳엔 들어 갈 수 없어.";
		next;
		mes "[도둑 길드 관계자]";
		mes "자네도 알다시피 이 곳은 도둑이 되고 싶어하는 초보자들의 전적시험장이거든.";
		mes "이제 이 비좁은 곳에서 벗어나 넓은 세계로 나가보지 그래?";
		close;
	}
	else if (BaseJob != Job_Novice && BaseJob != Job_Thief) {
		mes "[수상한 사람]";
		switch(rand(4)) {
		case 1:
			mes "....이봐요. 당신이 아무리 나에게 물어봤자 난 당신과 할 말이 없으니 당신 갈 길이나 가쇼.";
			close;
		case 2:
			mes "...............";
			close;
		case 3:
			mes "오, 저는∼ 수.상.한. 사람이. 아.닙.니.다.";
			mes "그.런.눈으로~ 쳐.다.보.지.말.아.주.세.요.";
			close;
		case 4:
			mes "'돌아갈 집이 있다는건 행복의 첫 걸음이다.'";
			mes "제가 가장 좋아하는 구절입니다. 돌아갈 집이라....";
			close;
		default:
			mes "어이구~ 안녕하세요. 정말 좋은 날씨입니다.";
			mes "오늘 같이 따뜻한 햇살이 내려째는 포근한 오후엔 피라미드 근처 그늘진 곳에 자리를 잡아 동료들과 점심을 함께하고 싶군요.";
			close;
		}
	}
	if (job_thief_q == 3) {
		mes "[도둑 길드 관계자]";
		mes "으하하하~~ 뭐야~~ 너 아직도 합격 못 한거야!? 바보아냐!! 크하하하!!";
		mes "아, 참!! 도둑길드엔 원래 바보들만 오는 곳이지!! 크하하!!";
		mes "좋아! 보내주마!";
		close2;
		switch(rand(5)) {
		case 1: warp "job_thief1",228,106; end;
		case 2: warp "job_thief1",38,50; end;
		case 3: warp "job_thief1",66,331; end;
		case 4: warp "job_thief1",196,331; end;
		default: warp "job_thief1",309,234; end;
		}
	}
	else if (job_thief_q == 2) {
		mes "[도둑 길드 관계자]";
		mes "네 녀석..... 묘한 냄새가 나는 것으로 보아하니.... 전직시험 보러왔군.";
		next;
		mes "[도둑 길드 관계자]";
		mes "이름은? " + strcharinfo(0) + " 이라... 에.... 명단에 있군!";
		mes "좋아! 보내주마! 하지만 어디로 떨어질지는 나도 몰라!! 끼앗호!";
		close2;
		set job_thief_q,3;
		switch(rand(5)) {
		case 1: warp "job_thief1",228,106; end;
		case 2: warp "job_thief1",38,50; end;
		case 3: warp "job_thief1",66,331; end;
		case 4: warp "job_thief1",196,331; end;
		default: warp "job_thief1",309,234; end;
		}
	}
	else if (job_thief_q == 1) {
		mes "[수상한 사람]";
		mes "너... 수상한데? 왜 여길 온거야?";
		close;
	}
	mes "[수상한 사람]";
	mes "거기의 초보자! 훌륭하다";
	mes "인생을 즐겁게 보내고 싶은가? 좀 더 편한 생활을";
	mes "하고 싶은가? (이)라면 도둑이 되어라! ";
	mes "그리고 도둑길드에 입단한다! ";
	next;
	mes "[수상한 사람]";
	mes "도둑길드는 언제나 자네들을";
	mes "환영한다! 금방 가입하면(자)";
	mes "포인트 카드 첨부! (농담)";
	mes "장소는, 피라미드 지하 감옥 1층";
	mes "중앙의 계단을 내려가 왼쪽이다!";
	mes "...오른쪽일지도.";
	close;
}


//============================================================
// Monsters
//============================================================
job_thief1,0,0,0,0	monster	주홍버섯	1182,180,0,0,0
job_thief1,0,0,0,0	monster	파브르	1184,50,0,0,0
job_thief1,0,0,0,0	monster	촌촌	1183,50,0,0,0
job_thief1,0,0,0,0	monster	스포어	1014,30,0,0,0

//============================================================
// mapflag
//============================================================
job_thief1	mapflag	nomemo
job_thief1	mapflag	noteleport
job_thief1	mapflag	nosave	SavePoint
job_thief1	mapflag	nopenalty
job_thief1	mapflag	nobranch
