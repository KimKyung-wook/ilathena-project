
//    kk  ""tt""  "";;    """"JJ;;kkJJ  JJtt""    ""tt""  ;;tt""""tt    ""tt""   
//   JJ  kk""tt;;JJ      ttkk    ""tt    ;;;;ttJJ;;  ""ttkk;;"";;;;ttkk;;  ;;kk  
//  "";;  ""  kk  kk  tt  ttww    ZZ;;  ww    RR  ;;JJ  JJJJ  tt    RR  ;;JJ  JJ 
//  JJ      """";;""  kk  ttkk  ;;kk    ZZ    ww  ;;""  kk""  ww    kk;;kk""  JJ 
//  kk      JJ  kk    ww  ttkk  JJJJ  ;;ww  ""JJ  ;;tt""RR    ZZ  ;;ZZJJ      kk 
//  kk  ;;  "";;JJ        JJtt  JJJJ  ""kk  JJ""  ww""""ww    ww  ""JJ  ZZ    kk 
//  JJ  JJ  ;;JJ""  "";;  ww;;  kkJJ  JJ""  kk;;  ww  ;;ww  ""JJ  ww;;  ww  ;;tt 
//;;""  RR    RR    ww""  kk""  ;;tt  ww    kkkk      kktt  JJ;;  ZZ;;  ;;  tt   
//  JJkkttkkkk;;kkkk;;JJkktt""kkJJJJkkttJJkk;;  kkkkJJ  ttkkttJJkk;;ttkkJJkkJJ   
//| I | - | L | U | N | A | A | T | H | E | N | A | P | R | O | J | E | C | T |  
//------------------------------------------------------------------------------
// iLAthena 복사 전직 스크립트 <http://www.i-luna.com>
// 한글화 by 아이루나 iLAthena Project
//------------------------------------------------------------------------------
// 한글화 by 인즈
// 수정 by 불근앙Ma
//------------------------------------------------------------------------------

prt_church,184,41,4	script	마르시스 신부#aco	60,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Priest || ADVJOB == Job_Champion) {
			if (Class == Job_Novice_High) {
				mes "[마르시스 신부]";
				mes "오, 형제여. 그대는 마치 과거 삶을 경험하여 견뎌낸 듯 하군요";
				mes "그대는 분명 발할라에 가기 전에 많은 것을 배웠겠지요.";
				next;
				if (getskilllv("NV_BASIC") < 9) {
					mes "[마르시스 신부]";
					mes "불행히도, 당신은 아직 복사가 되기 위한 준비가 덜 된 것같습니다. 먼저 기본기능을 모두 올려주길 바랍니다.";
					next;
					mes "[마르시스 신부]";
					mes "당신이 준비가 될 때까지 기다리고 있겠습니다";
					mes "주신의 축복이 함께하길.";
					close;
				}
				mes "[마르시스 신부]";
				mes "발할라에서 돌아온 것과 새로운 삶의";
				mes "여행을 하게 된 것을 축복하네.";
				next;
				skill 143,0,0;
				jobchange Job_Acolyte_High;
				skill 156,1,0;
				mes "[마르시스 신부]";
				mes "앞으로의 모험에도 누군가 자네의 도움을 바랄 것이네. 주신께서 자네의 앞길을 비추시길.";
				close;
			}
			else {
				mes "[마르시스 신부]";
				mes "앞으로의 모험에도 누군가 자네의 도움을 바랄 것이네. 주신께서 자네의 앞길을 비추시길.";
				close;
			}
		}
		else {
			mes "[마르시스 신부]";
			mes "오, 형제여. 그대는 마치 과거 삶을 경험하여 견뎌낸 듯 하군요";
			mes "그대는 분명 발할라에 가기 전에 많은 것을 배웠겠지요.";
			next;
			mes "[마르시스 신부]";
			mes "하지만 나는 당신이 복사가 될 수 있다고는 말할 수 없습니다. 그대의 과거의 삶에서 당신이 누구였고, 길이 무엇이었는지를 기억해내기를.";
			close;
		}
	}
	mes "[마르시스 신부]";
	mes "무슨 일로 찾아오셨습니까?";
	next;
	switch(select("복사로 전직한다.:복사 전직조건에 대해 묻는다.:그만둔다.")) {
	case 1:
		mes "[마르시스 신부]";
		if (getskilllv("NV_BASIC") < 9) {
			mes "아니?! 아직도 기본적인 수련조차 하지 않으셨단 말입니까?! 아직 멀었소!";
			next;
			mes "[마르시스 신부]";
			mes "좀 더 높은 직업 레벨을 필요로 합니다!";
			close;
		}
		if (BaseJob == Job_Acolyte) {
			mes "형제께선 혹시 어디가 편찮으신지..? 그 옷차림새는.. 이미 복사 아닌가요? 혹시 그걸 농담이라고 한건 아니겠지요?";
			close;
		}
		else if (BaseJob != Job_Novice) {
			mes "형제께선 이미 다른 직업에 종사하고 계시지 않습니까?";
			close;
		}
		if (job_acolyte_q == 0) {
			mes "형제께서는 진심으로 신의 종이 되려 하십니까?";
			next;
			if (select("복사 전직 신청을 한다.:그만둔다.") == 1) {
				mes "[마르시스 신부]";
				mes "좋습니다. "+strcharinfo(0)+"  형제의 복사 전직 신청서가 접수 되었습니다.";
				mes "형제께선 신의 종이 되기 위해서는 수련의 단계를 거쳐야 한다는 사실을 알고 계시겠지요?";
				next;
				mes "[마르시스 신부]";
				mes "형제께선.....";
				switch(rand(3)) {
				default:
					set job_acolyte_q,2;
					mes "^000077프론테라 북동쪽 어딘가의 유적 근처^000000에서 수행중이신 프론테라 교구의 ^000077루발카바라^000000 신부님께 다녀오십시오.";
					break;
				case 1:
					set job_acolyte_q,3;
					mes "^000077프론테라 남서쪽 모로크 마을 근처^000000에서 수행중이신 ^000077마틸다^000000 수녀님께 다녀오십시오.";
					break;
				case 2:
					set job_acolyte_q,4;
					mes "^000077프론테라 북서쪽 어딘가의 다리^000000에서 수행중이신 ^000077요우스케^000000 신부님께 다녀오십시오.";
					break;
				}
				next;
				mes "[마르시스 신부]";
				mes "그럼 기다리고 있겠습니다.";
				close;
			}
			mes "[마르시스 신부]";
			mes "...";
			close;
		}
		mes "[마르시스 신부]";
		mes "오, 형제여. 다시 찾아오셨군요. 과연 하늘의 뜻을 수행할 정도의 준비가 되었는지... 어디 봅시다...";
		next;
		mes "[마르시스 신부]";
		if (job_acolyte_q < 5) {
			mes "형제의 이름을 수행 완수자 명단에서 찾아볼 수 없군요.";
			next;
			switch(job_acolyte_q) {
			case 2:
				mes "[마르시스 신부]";
				mes "^000077프론테라 북동쪽 어딘가의 유적 근처^000000에서 수행중이신 프론테라 교구의 ^000077루발카바라^000000 신부님께 다녀오십시오.";
				break;
			case 3:
				mes "[마르시스 신부]";
				mes "^000077프론테라 남서쪽 모로크 마을 근처^000000에서 수행중이신 ^000077마틸다^000000 수녀님께 다녀오십시오.";
				break;
			case 4:
				mes "[마르시스 신부]";
				mes "^000077프론테라 북서쪽 어딘가의 다리^000000에서 수행중이신 ^000077요우스케^000000 신부님께 다녀오십시오.";
				break;
			}
			next;
			mes "[마르시스 신부]";
			mes "이 두 가지를 완벽히 수행했다고 생각되면 다시 이곳으로 오도록 하십시오. 그러면 신의 의지를 펼쳐보이는 성스러운 직업을 갖게 될 것입니다.";
			close;
		}
		mes "으흠... 직업 레벨도 충분하고 수행 완수자 명단에도 들어오셨으니... 좋습니다.";
		next;
		mes "[마르시스 신부]";
		mes " 형제의 의지를 받아들여 형제에게 복사 자격을 드리겠습니다.";
		next;
		skill 143,0,0;
		callfunc "Job_Change",Job_Acolyte;
		callfunc "F_ClearJobVar";
		mes "[마르시스 신부]";
		mes "언제나 주신께서 우리를 돌보심을 감사히 기억하길 바랍니다.";
		next;
		mes "[마르시스 신부]";
		mes "그분께서 형제에게 준 힘은 남을 도우라는 것입니다. 어지러운 혼돈 속에 있더라도, 당신의 확고한 믿음으로 그것에 맞서기 바랍니다.";
		next;
		mes "[마르시스 신부]";
		mes "끝으로 나는 진정으로 형제님이 참을성있게 시련을 겪어낸 것을 축하할 수 있게 되기를 바랍니다.";
		close;
	case 2:
		mes "형제, 복사가 되고 싶다고 하셨습니까?";
		mes "그렇다면 제가 말할 이 세가지 조건들을 신이 내려주신 수련으로 생각하고 견뎌내셔야 합니다.";
		next;
		mes "[마르시스 신부]";
		mes "첫 번째, 초보자 직업 스킬을 기본 레벨 9 까지 올려야 합니다.";
		mes "이 세계에서 살아가기 위해선 가장 기본적으로 수행해야 하는 일이니 자신의 일이라 생각하고 수련하도록 하십시오.";
		next;
		mes "[마르시스 신부]";
		mes "두 번째, 수행자로써 시련을 극복하고 와야 합니다.";
		next;
		mes "[마르시스 신부]";
		if (job_acolyte_q != 0) {
			switch(job_acolyte_q) {
			case 2:
				mes "^000077프론테라 북동쪽 어딘가의 유적 근처^000000에서 수행중이신 프론테라 교구의 ^000077루발카바라^000000 신부님께 다녀오십시오.";
				break;
			case 3:
				mes "^000077프론테라 남서쪽 모로크 마을 근처^000000에서 수행중이신 ^000077마틸다^000000 수녀님께 다녀오십시오.";
				break;
			default:
				mes "^000077프론테라 북서쪽 어딘가의 다리^000000에서 수행중이신 ^000077요우스케^000000 신부님께 다녀오십시오.";
				break;
			}
			next;
			mes "[마르시스 신부]";
			mes "이 두 가지를 완벽히 수행했다고 생각되면 다시 이곳으로 오도록 하십시오. 그러면 신의 의지를 펼쳐보이는 성스러운 직업을 갖게 될 것입니다.";
		}
		else {
			mes "수행지는 복사 전직 신청서를 작성하시면 부여 됩니다.";
		}
		next;
		mes "[마르시스 신부]";
		mes "이 두 가지를 완벽히 수행했다고 생각되면 다시 이곳으로 오도록 하십시오. 그러면 신의 의지를 펼쳐보이는 성스러운 직업을 갖게 될 것입니다.";
		close;
	case 3:
		close;
	}
}

prt_fild03,365,255,2	script	수행중인 신부#aco	89,{
	mes "[루발카바라 신부]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 6) {
			mes "자네가 도착할 때 쯤이면 이미 연락이 되었을테니 조심해서 이만 돌아가보게나.";
			next;
			mes "[루발카바라 신부]";
			mes " 비둘기 다리에 쪽지를 묶어 날렸는데, 과연 잘 도착할런지...";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 2) {
				mes "아... 혹시, 자네가 복사가 되고 싶다는 젊은이인가..? 자네에 대한 연락은 이미 성당측에서 받았다네.";
				mes "자네 이름이.... "+strcharinfo(0)+".. 맞군! 그래, 무사히 잘 찾아와주었네.";
				next;
				mes "[루발카바라 신부]";
				mes "이미 마르시스 신부에게 복사에 대한 여러 이야기를 들었을테니 더이상 말할 필요는 없겠지. 요즘 젊은이들은 잔소리를 그다지 좋아하지 않지 않은가? 하하...";
				next;
				mes "[루발카바라 신부]";
				mes "하지만 신을 수행하려면 그정도는 참아야 할런지도 몰라. 왜냐하면 신은 그다지 과묵하지 않기때문이지.";
				mes "어찌되었든, 자네가 이 곳에 들렸다는 확인서을 보낼테니 그리 알고 다시 프론테라로 떠나게나.";
				next;
				mes "[루발카바라 신부]";
				mes "부디 몸 조심 하게나.";
				close2;
				savepoint "prt_fild03",361,255;
				set job_acolyte_q,6;
				end;
			}
			else {
				mes "자네는 복사가 되길 원하는 사람들 중 하나인가?";
				mes "어디보자...";
				next;
				mes "[루발카바라 신부]";
				mes "자네 이름이... " + strcharinfo(0) + "?";
				mes "나는 자네 이름을 몰라, 나는 연락을 받지 못했다네.";
				next;
				mes "[루발카바라 신부]";
				mes "프론테라 성당으로 되돌아가 다시 한번 알아보는 것이 어떻겠나?";
				close;
			}
		}
		else {
			mes "여기에는 왜 온건가? 여기는 자네같은 노비스에게는 매우 위험한 장소라구!";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherRub";
	else {
		if (BaseJob == Job_Priest) {
			mes "반갑네.";
			next;
			mes "[루발카바라 신부]";
			mes "깊은 곳까지 잘 왔네. 편안하게 앉아 나와 함께 신의 메세지를 묵상하세. 이곳의 경치는 무척 아름다워서 위험이 동반되더라도 침착하게 느껴질 정도지...";
			close;
		}
		else {
			mes "허어....";
			mes "이 깊은 곳까지 자네는 훈련을 하러 들어온 건가? 아니면 단지 방랑자일 뿐인가?";
			next;
			mes "[루발카바라 신부]";
			mes "자네가 누구든, 몸 조심하게. 이곳의 몬스터들은 그들의 외모와는 안 어울리게 무서울 정도로 강하다네.";
			close;
		}
	}
}

moc_fild07,41,355,4	script	수행중인 수녀#2aco	95,{
	mes "[마틸다 수녀]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 7) {
			mes "비둘기를 이용하여 확인서를 보낼테니 형제님께서 성당에 도착하실 쯤이면 이미 담당 신부님께서 연락을 받으셨을 겁니다.";
			mes "형제님께서 바라시던 복사의 길을 걷게 되도록 신께 기도 드리겠습니다.";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 3) {
				mes "아, 복사 수행자 이시군요. 이곳까지 잘 찾아오셨네요.";
				next;
				mes "[마틸다 수녀]";
				mes "성함이 어떻게 되시죠? 에... "+strcharinfo(0)+" 형제님.. 아, 네! 여기 있네요!";
				mes ""+strcharinfo(0)+" 형제님 께서 이 곳에 오셨다는 확인서를 보내겠습니다.";
				mes "성당으로 돌아가셔서 담당 신부님께 확인해 주세요.";
				close2;
				savepoint "moc_fild07",35,355;
				set job_acolyte_q,7;
				end;
			}
			else {
				mes "아, 복사 수행자 이시군요. 이곳까지 잘 찾아오셨네요.";
				next;
				mes "[마틸다 수녀]";
				mes "성함이 어떻게 되시죠? 에... "+strcharinfo(0)+" 형제님..";
				next;
				mes "[마틸다 수녀]";
				mes "어디보자...";
				mes "형제님의 이름은 제 리스트에 없으시네요.";
				next;
				mes "[마틸다 수녀]";
				mes "아마도 다시 한번 프론테라 성당으로 돌아가셔서 수행장소가 어디인지 알아보는 것이 나을 것 같군요.";
				close;
			}
		}
		else {
			mes "...";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_MotherMart";
	else {
		if (BaseJob == Job_Priest) {
			mes "어서오세요.";
			next;
			mes "[마틸다 수녀]";
			mes "당신의 수행은 어느 정도나 진행 되었습니까? 신의 은총이 깃든 삶을 누리며 즐거이 보내기를.";
			close;
		}
		else {
			mes "당신에게 신의 가호가 있길...";
			close;
		}
	}
}

prt_fild00,208,218,6	script	수행중인 신부#3aco	98,{
	mes "[요스케 신부]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 8) {
			mes "뭐지? 나에게 더 이상 볼 일이 있나?";
			mes "없잖아! 성당으로 돌아가기나 해!";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 4) {
				mes "거기. 복사 수행자.... 인가.";
				next;
				mes "[요스케 신부]";
				mes "숨이 붙어있는 채로 잘도 찾아왔군.";
			  	mes "네 이름은? "+strcharinfo(0)+" 이라.";
				next;
				mes "[요스케 신부]";
				mes "좋아. "+strcharinfo(0)+". 이 곳에 네가 왔었다는 확인서를 보내주겠다.";
				mes "성당으로 돌아가서 담당 신부님께 확인해라.";
				close2;
				savepoint "prt_fild00",206,230;
				set job_acolyte_q,8;
				end;
			}
			else {
				mes "거기. 복사 수행자.... 인가.";
				next;
				mes "[요스케 신부]";
				mes "숨이 붙어있는 채로 잘도 찾아왔군.";
			  	mes "네 이름은? "+strcharinfo(0)+" 이라.";
				next;
				mes "[요스케 신부]";
				mes "미안하지만 잘못왔다. 성당으로 돌아가서, 주교에게 다시 확인해라.";
				close;
			}
		}
		else {
			mes "너... 노비스로군.";
			mes "뭔가 말할 것이라도 있나?";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherYos";
	else {
		if (BaseJob == Job_Priest) {
			mes "이봐...";
			next;
			mes "[요스케 신부]";
			mes "만약 자네만 괜찮다면 이리와 나와 같이 앉아서 진리에 대해 명상하지 않겠나. 신의 위엄은 정말로 깨달음을 주는 것이라...";
			close;
		}
		else {
			mes "응? 성당에 돌아가지 않는 것인지? ";
			mes "그렇지 않으면 여기서 일생을 보내고 싶은 것인지? ";
			close;
		}
	}
}
