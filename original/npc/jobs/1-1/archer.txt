
//    kk  ""tt""  "";;    """"JJ;;kkJJ  JJtt""    ""tt""  ;;tt""""tt    ""tt""   
//   JJ  kk""tt;;JJ      ttkk    ""tt    ;;;;ttJJ;;  ""ttkk;;"";;;;ttkk;;  ;;kk  
//  "";;  ""  kk  kk  tt  ttww    ZZ;;  ww    RR  ;;JJ  JJJJ  tt    RR  ;;JJ  JJ 
//  JJ      """";;""  kk  ttkk  ;;kk    ZZ    ww  ;;""  kk""  ww    kk;;kk""  JJ 
//  kk      JJ  kk    ww  ttkk  JJJJ  ;;ww  ""JJ  ;;tt""RR    ZZ  ;;ZZJJ      kk 
//  kk  ;;  "";;JJ        JJtt  JJJJ  ""kk  JJ""  ww""""ww    ww  ""JJ  ZZ    kk 
//  JJ  JJ  ;;JJ""  "";;  ww;;  kkJJ  JJ""  kk;;  ww  ;;ww  ""JJ  ww;;  ww  ;;tt 
//;;""  RR    RR    ww""  kk""  ;;tt  ww    kkkk      kktt  JJ;;  ZZ;;  ;;  tt   
//  JJkkttkkkk;;kkkk;;JJkktt""kkJJJJkkttJJkk;;  kkkkJJ  ttkkttJJkk;;ttkkJJkkJJ   
//| I | - | L | U | N | A | A | T | H | E | N | A | P | R | O | J | E | C | T |  
//------------------------------------------------------------------------------
// iLAthena 궁수 전직 스크립트 <http://www.i-luna.com>
// 한글화 by 아이루나 iLAthena Project
//------------------------------------------------------------------------------
// 한글화 by 인즈
// 수정 by 러쉬
//------------------------------------------------------------------------------

payon_in02,64,71,4	script	길드 관계자#archer	85,{
	if (Upper == 1) {
		if (Class == Job_Novice_High && ADVJOB == Job_Sniper || ADVJOB == Job_Clown || ADVJOB == Job_Gypsy) {
			mes "[궁수 길드 관계자]";
			mes "...언젠가 본 것 같군.";
			mes "이전에도 전직 시험을 본 것 같은데, 아닌가?";
			next;
			mes "[궁수 길드 관계자]";
			mes "너는 분명 발할라에 다녀온 것 같군.";
			mes "그리고 다시 태어난 거야..";
			mes "이건, 대단하군!";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "[궁수 길드 관계자]";
				mes "으음...";
				mes "궁수가 되기 전에 먼저 기본 스킬을 올리고 오는 것이 어떻겠나?.";
				next;
				mes "[궁수 길드 관계자]";
				mes "그럼, 나중에 보세.";
				close;
			}
			mes "[궁수 길드 관계자]";
			mes "어쨌든 자네에겐 같은 말을 할 필요는 없는 것 같군.";
			mes "나는 자네가 훌륭한 궁수였음을 알고 있으니.";
			next;
			skill 143,0,0;
			jobchange Job_Archer_high;
			skill 147,1,0;
			skill 148,1,0;
			mes "[궁수 길드 관계자]";
			mes "비록 대단한 보상은 없지만";
			mes "나는 자네에게 기대하고 있다네. 몸 조심하게.";
			close;
		}
		else {
			mes "[궁수 길드 관계자]";
			mes "음...?";
			mes "자네는 여기서 뭘 하는 건가?";
			next;
			mes "[궁수 길드 관계자]";
			mes "자네는 궁수가 될 수 없네. 자네는 활을 쏘는 쪽의 인물이 아니었던 것 같군.";
			close;
		}
	}
	mes "[궁수 길드 관계자]";
	mes "안녕하세요, 무엇을 도와드릴까요?";
	next;
	switch(select("궁수로 전직한다.:궁수 전직 조건을 듣는다.:아무 것도...")) {
	case 1:
		if (BaseJob == Job_Archer) {
			mes "[궁수 길드 관계자]";
			mes "...당신은 이미 궁수입니다.";
			close;
		}
		else if (BaseJob != Job_Archer && BaseJob != Job_Novice) {
			mes "[궁수 길드 관계자]";
			mes "흠..";
			mes "당신은 직업을 가진 것 같군요.";
			next;
			mes "[궁수 길드 관계자]";
			mes "당신은 이미 직업을 가졌으니, 궁수가 될 수 없습니다.";
			close;
		}
		if (job_archer_q == 0) {
			mes "[궁수 길드 관계자]";
			mes "궁수로 전직을 원하십니까?";
			mes "그렇다면, 여기서 전직 신청을 해 주세요. ";
			next;
			if (select("전직 신청을 한다.:그만둔다") == 1) {
				set job_archer_q,1;
				mes "[궁수 길드 관계자]";
				mes "신청서에 싸인을 해주세요.";
				mes "네. 이것으로 신청은 OK입니다. ";
				next;
				mes "[궁수 길드 관계자]";
				mes "지금부터 궁수가 되기 위한 조건을";
				mes "모두 채우면, 전직이 가능합니다. ";
				mes "이미 전직의 조건을 채우고 있는지,";
				mes "지금 확인합니다만… 좋습니까? ";
				next;
				if (select("네.:아니오.") == 1) {
					mes "[궁수 길드 관계자]";
					mes "확인 하겠습니다.";
					next;
				}
				else {
					mes "[궁수 길드 관계자]";
					mes "알겠습니다.";
					mes "궁수 전직 조건에 대해";
					mes "모르는 부분이 있으면,";
					mes "저에게 물어 주세요. ";
					close;
				}
			}
			else {
			mes "[궁수 길드 관계자]";
			mes "그럼 다음에 찾아오세요.";
			close;
			}
		}
		mes "[궁수 길드 관계자]";
		mes "이름이.." + strcharinfo(0) + "?";
		next;
		if (getskilllv("NV_BASIC") < 9) {
			mes "[궁수 길드 관계자]";
			mes "아직 초보자 스킬 레벨이 충분하지 않습니다. ";
			mes "전직 조건을 한번 더 확인한 다음,";
			next;
			mes "[궁수 길드 관계자]";
			mes "초보자 스킬 레벨 9까지";
			mes "마스터 해 주세요. ";
			close;
		}
		if (job_archer_q == 1) {
			set .@archer_item1,countitem(1066) * 5;
			set .@archer_item2,countitem(1067) * 3;
			set .@archer_item3,countitem(1068) * 2;
			set .@archer_item4,countitem(1019);
			set .@total_archer,.@archer_item1 + .@archer_item2 + .@archer_item3 + .@archer_item4;
			set .@total_archer2,(((.@archer_item2 + .@archer_item3) * 2) + .@archer_item4);
			mes "[궁수 길드 관계자]";
			mes "초보조 스킬은 충분하군요.";
			mes "활제작에 필요한 나무조각의 채점을 시작합니다. ";
			mes "어디보자...";
			next;
			next;
			if (countitem(1066) == 0 && countitem(1067) == 0 && countitem(1068) == 0 && countitem(1019) == 0) {
				mes "[궁수 길드 관계자]";
				mes ".....";
				mes "채점할 나무조각이 하나도 없는 것 같군요.";
				close;
			}
			mes "[궁수 길드 관계자]";
			if (countitem(1066) != 0) mes " 결이 고운 나무 조각 : " + countitem(1066) + " 개, 점수 : " + .@archer_item1 + " . ";
			if (countitem(1067) != 0) mes " 단단한 나무 조각 : " + countitem(1067) + " 개, 점수 : " + .@archer_item2 + " . ";
			if (countitem(1068) != 0) mes " 메마른 나무 조각 : " + countitem(1068) + " 개, 점수 : " + .@archer_item3 + " . ";
			if (countitem(1019) != 0) mes " 나무 조각 : " + countitem(1019) + " 개, 점수 : " + .@archer_item4 + " . ";
			if (.@total_archer < 25) {
				mes "총점: ^FF0000" + .@total_archer + "^000000 / 40";
				next;
				mes "[궁수 길드 관계자]";
				mes "25점이하? ";
				mes "25점이상이 되지 않으면,";
				mes "합격이 되지 않습니다! ";
				mes "좀 더 노력해 주세요! ";
				close;
			}
			else {
				mes "총점: ^FF0000" + .@total_archer + "^000000 / 40";
				next;
				mes "[궁수 길드 관계자]";
				if (.@total_archer > 40) {
					mes "40점 이상이군요!";
					mes "굉장합니다! 합격입니다!";
				}
				else if (.@total_archer > 30) {
					mes "30점 이상이군요!";
					mes "합격입니다!";
				}
				else if (.@total_archer > 25) {
					mes "*씨익*";
					mes "아슬아슬하게 합격이군요...";
					mes "아무튼, 고생하셨습니다.";
				}
			}
			next;
			mes "[궁수 길드 관계자]";
			mes "시험을 통과하셨으므로, 전직 시켜드리겠습니다!";
			if (countitem(1066) != 0) delitem 1066,countitem(1066);
			if (countitem(1067) != 0) delitem 1067,countitem(1067);
			if (countitem(1068) != 0) delitem 1068,countitem(1068);
			if (countitem(1019) != 0) delitem 1019,countitem(1019);
		}
		next;
		callfunc "Job_Change",Job_Archer;
		callfunc "F_ClearJobVar";
		mes "[궁수 길드 관계자]";
		mes "궁수로의 전직을 축하합니다!";
		next;
		mes "[궁수 길드 관계자]";
		mes "앞으로도 궁수 길드의";
		mes "부흥을 위해 노력해 주세요! ";
		next;
		mes "[궁수 길드 관계자]";
		mes "아, 제작 담당으로부터 선물이";
		mes "왔습니다. 받아 주세요. ";
		getitem 1702,1; //활
		getitem 1750,.@total_archer2; // 화살
		next;
		mes "[궁수 길드 관계자]";
		mes "궁수는 역시";
		mes "활과 화살을 가지지 않으면! ";
		mes "그러면, 노력해 주세요. ";
		mes "기대하고 있습니다. ";
		close;
	case 2:
		mes "[궁수 길드 관계자]";
		if (BaseJob != Job_Novice) {
			if (BaseJob == Job_Archer) {
				next;
				mes "...";
				mes "당신은 이미 궁수입니다만...";
				close;
			}
			else {
				next;
				mes "당신은 이미 다른 직업에 종사하고 있습니다만...";
				close;
			}
		}
		mes "그러면, 궁수의 전직 조건에";
		mes "대해 설명합니다. ";
		next;
		mes "[궁수 길드 관계자]";
		mes "우선, 초보자 스킬 레벨을";
		mes "9이상으로 해 주세요. ";
		next;
		mes "[궁수 길드 관계자]";
		mes "궁수 전직 시험에서는, 활의 제작에";
		mes "필요한 ^ff0000나무조각^000000 을 모아 와 주세요. ";
		mes "가져온 나무조각은 4개의 종류로";
		mes "분류되어 그 수와 합해 채점됩니다.";
		next;
		mes "[궁수 길드 관계자]";
		mes "나무조각 아이템은 윌로우로부터 얻을 수 있습니다.";
		mes "약한 몬스터이지만, 방심은 금물입니다. ";
		next;
		mes "[궁수 길드 관계자]";
		mes "40점 만점 중^0000ff 25점이상^000000이면,";
		mes "궁수 전직이 가능합니다. ";
		close;
	case 3:
		close;
	}
}
